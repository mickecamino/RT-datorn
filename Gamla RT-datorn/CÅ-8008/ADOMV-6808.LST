                              NAM     ADOMV
                              OPT     M08
                      *
                      * Programexempel för A/D-omvandlare
                      * CÅ-8008 till RT-datorn
                      *
                      * Av Åke Holm 80-06-25
                      *
                      * Programmet hämtar de analoga
                      * värdena från ingång 0 och 1 och
                      * omvandlar dessa till decimala värden
                      * mellan 0 och 255. dessa värden läggs
                      * ut på de sex lysdiodsindikatorerna
                      * på displaykort CÅ-8006
                      *
 8080                 PIAAD0  EQU     $8080   DATAREGISTER ADRESSVAL
 8081                 PIAAD1  EQU     $8081   KONTROLLRED. DITO
 8082                 PIAAD2  EQU     $8082   DATAREGISTER MÄTVÄRDE
 8083                 PIAAD3  EQU     $8083   KONTROLLREG. DITO
 80E0                 DISPL   EQU     $80E0   ADRESS TILL FÖRSTA DISPLA
                      *
                              ORG     $A100
                      *
 A100 7F8081          START   CLR     PIAAD1  NOLLA KONTROLLREG.
 A103 7F8083                  CLR     PIAAD3
 A106 7F8082                  CLR     PIAAD2  PB0-7 INGÅNGAR
 A109 8636                    LDA     #$36    CB2 UTGÅNG, CB1 INGÅNG
 A10B B78083                  STA     PIAAD3
 A10E 860F                    LDA     #$0F    PA0-3 UTGÅNGAR
 A110 B78080                  STA     PIAAD0
 A113 8604                    LDA     #4
 A115 B78081                  STA     PIAAD1
                      *
 A118 5F              LOOP00  CLRB            NOLLA FÖR INGÅNG 0
 A119 CEA1A3                  LDX     #BINTAB
 A11C 8D28                    BSR     INPUT   HÄMTA MÄTVÄRDE KANAL 0
 A11E 8D3C                    BSR     BCDCON  OMVANDLA TILL DECIMALT
 A120 A602                    LDA     2,X
 A122 B780E0                  STA     DISPL   LÄGG UT PÅ FÖRSTA DISPLAYEN
 A125 A603                    LDA     3,X     HÄMTA 2:A SIFFRAN
 A127 B780E1                  STA     DISPL+1 LÄGG UT PÅ 2:A DISPLAYEN
 A12A A604                    LDA     4,X     HÄMTA 3:E SIFFRAN
 A12C B780E2                  STA     DISPL+2 LÄGG UT PÅ 3:E DISPLAYEN
 A12F C601                    LDB     #1      HÄMTA FRÅN KANAL 1
 A131 8D13                    BSR     INPUT
 A133 8D27                    BSR     BCDCON
 A135 A602                    LDA     2,X     HÄMTA 1:A SIFFRAN
 A137 B780E3                  STA     DISPL+3 LÄGG UT PÅ 4:E DISPLAYEN
 A13A A603                    LDA     3,X     HÄMTA 2:A SIFFRAN
 A13C B780E4                  STA     DISPL+4 LÄGG UT PÅ 5:E DISPLAYEN
 A13F A604                    LDA     4,X     HÄMTA 3:E SIFFRAN
 A141 B780E5                  STA     DISPL+5 LÄGG UT PÅ 6:E DISPLAYEN
 A144 20D2                    BRA     LOOP00  FORTSÄTT
                      *
 A146 F78080          INPUT   STAB    PIAAD0
 A149 863E                    LDA     #$3E    GE HÖG PULS PÅ CB2
 A14B C636                    LDB     #$36
 A14D B78083                  STA     PIAAD3  HÖG NU
 A150 F78083                  STAB    PIAAD3  LÅG IGEN
 A153 B68083          INPUTS  LDA     PIAAD3  HÄMTA STATUS
 A156 2AFB                    BPL     INPUTS  VÄNTA TILLS AD-CONV. KLAR
 A158 B68082                  LDA     PIAAD2  HÄMTA MÄTVÄRDE
 A15B 39                      RTS
                      *
                      * RUTIN FÖR OMVANDLING AV EN
                      * HEX-BYTE TILL DECIMALTAL 0-255
                      * A-ACC LSB HALVA
                      * B-ACC MSB HALVA
                      *
 A15C FFA1A1          BCDCON  STX     SAVEX   SPARA PEKAREN
 A15F CEA194                  LDX     #CONST  TABELL FÖR KONSTANTEN
 A162 5F                      CLRB
 A163 7FA19E          B10     CLR     SAVEA   ATT BILDA DECIMALTECKEN
 A166 A001            B20     SUBA    1,X     DRAG IFRÅN TILLS UNDERFLOW
 A168 E200                    SBCB    0,X
 A16A 2505                    BCS     B30
 A16C 7CA19E                  INC     SAVEA
 A16F 20F5                    BRA     B20
                      *
 A171 AB01            B30     ADDA    1,X
 A173 E900                    ADCB    0,X
 A175 36                      PSHA
 A176 FFA19F                  STX     SAVEK0  SPARA AKTUELLT VÄRDE
 A179 FEA1A1                  LDX     SAVEX   VAR SKALL DET LAGRAS
 A17C B6A19E                  LDA     SAVEA   LÄGG IN DET
 A17F A700                    STA     0,X     I TABELLEN
 A181 32                      PULA            ÅTERTAG
 A182 08                      INX
 A183 FFA1A1                  STX     SAVEX   SPARA NYA VÄRDET
 A186 FEA19F                  LDX     SAVEK0
 A189 08                      INX
 A18A 08                      INX
 A18B 8CA19E                  CPX     #KONEND SLUT PÅ KONSTANTERNA ?
 A18E 26D3                    BNE     B10
 A190 CEA1A3                  LDX     #BINTAB PEKA PÅ TABELLEN
 A193 39                      RTS
                      *
 A194 2710            CONST   FDB     10000
 A196 03E8                    FDB     1000
 A198 0064                    FDB     100
 A19A 000A                    FDB     10
 A19C 0001                    FDB     1
 A19E                 KONEND  EQU     *
                      *
 A19E                 SAVEA   RMB     1
 A19F                 SAVEK0  RMB     2
 A1A1                 SAVEX   RMB     2
 A1A3                 BINTAB  RMB     5
                      *
 A100                         END     START

SYMBOL TABLE
       B10 02 A163       B20 02 A166       B30 02 A171    BCDCON 02 A15C
    BINTAB 02 A1A3     CONST 02 A194     DISPL 00 80E0     INPUT 02 A146
    INPUTS 02 A153    KONEND 02 A19E    LOOP00 02 A118    PIAAD0 00 8080
    PIAAD1 00 8081    PIAAD2 00 8082    PIAAD3 00 8083     SAVEA 02 A19E
    SAVEK0 02 A19F     SAVEX 02 A1A1     START 02 A100
19 SYMBOLS

0 error(s), 0 warning(s)
