10  REM **************************
20  REM *  MASTERMIND F\R CT-82  *
30  REM * AV ]. STEN[NG 81-01-15 *
40  REM **************************
50 PRINT CHR$(28);CHR$(18):PRINT CHR$(29);CHR$(29);CHR$(31):REM FORMAT o BAUD
60 PRINT CHR$(30);CHR$(30):REM NUM KEYPAD
80 DIM A$(4),B$(4)
90 FOR I=1TO50:NEXTI:REM  DELAY F\R RAD 11
100 PRINT "                    * * MASTERMIND * *":PRINT:PRINT
110 ON ERROR GOTO 990
120 C$="*":D$="-":E$="0"
130 PRINT "DET G[LLER ATT GISSA R[TT P] 4 SIFFROR!"
140 PRINT "R[TT  SIFFRA  P] R[TT PLATS MARKERAS MED EN ASTERISK  --> * <--"
150 PRINT "R[TT SIFFRA MEN FEL PLACERAD MARKERAS MED ETT STRECK  --> - <--"
160 PRINT "UPP TILL 10 F\RS\K [R GODK[NT!      LYCKA TILL!"
170 DA=PEEK(HEX("ACOF"))
180 B=INT(9999*RND(0))+1:B=B+DA: REM B=RANDOMTALET. B+DA F\R OLIKA UPPSTARTNING.
190 IF LEN(STR$(B))<>6 GOTO180
200 PRINT
210 C=0: REM  C=ANTAL F\RS\K
220 E$=STR$(B): REM B OMVANDLAS TILL E$
230 E$=MID$(E$,2): REM F\R o EFTERSPACE TAS BORT
240 D=LEN(E$)-1
250 IF D=4 THEN300
260 D=4-D
270 FOR I=1 TO D
280 E$=E$+E$
290 NEXT I
300 IF C=10 AND D<4 THEN 970
310 C=C+1
320 PRINT"F\RS\K NR";TAB(11+2-LEN(STR$(C)));C;:INPUT A
330 REM ** CHR$(1)=FLYTTAR MARK\REN 1 RAD UPP, CHR$(9)=TILL H\GER 22 STEG,
340 REM ** F\R ATT F] *- P] SAMMA RAD VID VARJE INSKRIVNING! **
350 PRINT CHR$(1);:FOR Q=1TO22:PRINT CHR$(9);:NEXTQ
360 IFLEN(STR$(A))<>6 THENPRINTCHR$(7);:PRINT"   OBS! ANV[ND 4 SIFFROR!":GOTO320
370 G$=STR$(A)
380 G$=MID$(G$,2)
390 D=LEN(G$)-1
400 IF D=4 THEN450
410 D=4-D
420 FOR I=1TOD
430 G$=E$+G$
440 NEXT I
450 D=0: REM D= ANTAL R[TTA TECKEN (*-)
460 FOR I=1TO4
470 A$(I)=MID$(G$,I,1)
480 B$(I)=MID$(E$,I,1)
490 IF A$(I)<>B$(I) THEN530
500 A$(I)=C$
510 D=D+1
520 B$(I)="B"
530 NEXT I
540 IF D=4 THEN PRINT"****";:GOTO 910:REM  ALLA R[TT!
550 FOR I=1TO4
560 FOR J=1TO4
570 IF A$(I)<>B$(J) THEN610
580 A$(I)=D$
590 B$(J)="B"
600 J=5
610 NEXT J
620 NEXT I
630 H$=" "
640 K$=" "
650 FOR I=1TO4
660 IF A$(I)=C$ THEN690
670 IF A$(I)=D$ THEN710
680 GOTO 720
690 H$=H$+C$
700 GOTO 720
710 K$=K$+D$
720 NEXT I
730 D=LEN(H$)
740 IF D>1 THEN770
750 H$="N"
760 GOTO 780
770 H$=MID$(H$,2)
780 D=LEN(K$)
790 IF D>1 THEN840
800 K$="N"
810 IF H$=K$ THEN PRINT:GOTO 300
820 PRINT H$
830 GOTO 300
840 K$=MID$(K$,2)
850 IF H$<>"N" THEN880
860 PRINT K$
870 GOTO 300
880 L$=H$+K$
890 PRINT L$
900 GOTO 300
910 PRINT:PRINTCHR$(7):PRINT"R[TT SVARAT P] ";C;"F\RS\K!!"
920 PRINT
930 PRINT "VILL DU F\RS\KA IGEN?   ";
940 INPUT "SVARA JA ELLER NEJ (J/N)",P$
950 IF LEFT$(P$,1)="J" THEN PRINT CHR$(12):GOTO 90
960 END
970 PRINT:PRINTCHR$(7):PRINT"TYV[RR, DU HAR GISSAT";C;"G]NGER!  ";
980 PRINT "R[TT NUMMER [R";B:PRINT:GOTO930
990 IF ERR<>30 THEN ON ERROR GOTO 0
1000 PRINTCHR$(7);:PRINT"  OBS! ANV[ND ENDAST SIFFROR!"
1010 RESUME
