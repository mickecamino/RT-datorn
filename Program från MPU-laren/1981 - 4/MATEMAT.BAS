10 REM ***************************************************
20 REM *  MATEMATIK-SPEL F\R CT-82  OCH XBASIC. 81-05-25 *
30 REM *  PROGRAM-IDEN FR]N MICROCOMPUTING APRIL -81     *
40 REM *  AV ]KE STEN[NG, BANKERYD                       *
50 REM ***************************************************
60 PRINT CHR$(28);CHR$(18)
70 PRINT CHR$(29);CHR$(29);CHR$(31)
80 PRINT CHR$(30);CHR$(30)
90 POKE HEX("CC03"),0
100 FOR I%=1 TO 100:NEXT I%
110 PRINT"         * * M A T E M A T I K - S P E L * *"
120 PRINT
130 DIGITS 13,3
140 INPUT"VAD HETER DU ";N$
150 PRINT:PRINT"HEJ ";N$;", D] STARTAR VI MATTE-SPELET!":PRINT
160 PRINT"N[R DU VILL SLUTA SPELET, TRYCK 'S' !":PRINT
170 ON ERROR GOTO 1050
180 PRINT"    TRYCK  1  F\R ADDITION (plus)"
190 PRINT"    TRYCK  2  F\R SUBTRAKTION (minus)"
200 PRINT"    TRYCK  3  F\R MULTIPLIKATION (g}nger)"
210 PRINT"    TRYCK  4  F\R DIVISION (delat med)"
220 PRINT"    TRYCK  5  F\R LITE AV VARJE"
230 PRINT:INPUT"VAD V[LJER DU (1,2,3,4 eller 5) ";T
240 PRINT:INPUT"VILKET H\GSTA V[RDE VILL DU HA F\R DEN ENA SIFFRAN ";N
250 PRINT:INPUT"VILKET H\GSTA V[RDE VILL DU HA F\R DEN ANDRA SIFFRAN ";P
260 ON ERROR GOTO 1080
270 PRINT
280 REM * RAD 290,300 GER OLIKA V[RDE VID UPPSTARTNING
290 DA=PEEK(HEX("CC0F")):FOR I%=1TO DA:R1=RND(0):NEXT I%
300 FOR I%=1TO ASC(N$):R1=RND(0):NEXT I%
310 IF T< 5 THEN 340
320 G=1
330 T=INT(4*RND(0)+1):REM * GER OLIKA VAL VID 5 (lite av varje)
340 A=A+1
350 X=INT((N+1)*RND(0))
360 Y=INT((P+1)*RND(0))
370 ON T GOTO 390,430,510,550
380 REM *** ADDITION ***
390 Z1=X+Y
400 PRINT A;")   ";X;"+";Y;"= ";
410 GOTO 620
420 REM *** SUBTRAKTION ***
430 IF X>Y THEN 470
440 X1=Y
450 Y=X
460 X=X1
470 Z1=X-Y
480 PRINT A;")   ";X;"-";Y;"= ";
490 GOTO 620
500 REM *** MULTIPLIKATION ***
510 Z1=X*Y
520 PRINT A;")   ";X;"*";Y;"= ";
530 GOTO 620
540 REM *** DIVISION ***
550 IF X>0 THEN 580
560 X=Y
570 Y=0
580 L=X*Y
590 Z1=Y
600 PRINT A;")   ";L;"/";X;"= ";
610 REM *** SVAR ***
620 INPUT Z$
630 IF Z$="S" THEN 950
640 Z=VAL(Z$)
650 IF Z=Z1 THEN 790
660 R3=RND(0)
670 IF R3> .333 THEN 710
680 PRINT CHR$(1);:FOR W%=1 TO 35:PRINT CHR$(9);:NEXT W%
690 PRINT CHR$(7);:PRINT"FEL !! F\RS\K IGEN ";N$;" !":PRINT
700 GOTO 770
710 IF R3> .667 THEN 750
720 PRINT CHR$(1);:FOR W%=1 TO 35:PRINT CHR$(9);:NEXT W%
730 PRINT CHR$(7);:PRINT"FEL, JAG TROR DU KAN B[TTRE [N S] !":PRINT
740 GOTO 770
750 PRINT CHR$(1);:FOR W%=1 TO 35:PRINT CHR$(9);:NEXT W%
760 PRINT CHR$(7);:PRINT"FEL, ANSTR[NG HJ[RNAN LITE B[TTRE !":PRINT
770 F=1:C=C+1
780 ON T GOTO 400,480,520,600
790 R2=RND(0)
800 IF R2>.333 THEN 840
810 PRINT CHR$(1);:FOR W%=1 TO 35:PRINT CHR$(9);:NEXT W%
820 PRINT"GRATULERAR !!":PRINT
830 GOTO 900
840 IF R2> .667 THEN 880
850 PRINT CHR$(1);:FOR W%=1 TO 35:PRINT CHR$(9);:NEXT W%
860 PRINT"DU SK\TER DIG ";N$;" !!":PRINT
870 GOTO 900
880 PRINT CHR$(1);:FOR W%=1 TO 35:PRINT CHR$(9);:NEXT W%
890 PRINT"MYCKET BRA ";N$;" !!":PRINT
900 IF F=1 THEN 920
910 B=B+1
920 F=0
930 IF G=1 THEN 330
940 GOTO 340
950 REM * SLUT "
960 A=A-1:D=100*B/A
970 PRINT:PRINT
980  PRINT"SAMMANST[LLNING AV MATEMATIK-SPELET:":PRINT
990  PRINT"   TOTALA ANTALET PROBLEM    =";A
1000 PRINT"   TOTALA ANTALET R[TTA SVAR =";B
1010 PRINT"   TOTALA ANTALET FEL SVAR   =";C
1020 PRINT"   PROCENTUELLT R[TTA SVAR   =";D;"%"
1030 PRINT:PRINT"HEJ D] ";N$;" VI SES!"
1040 END
1050 IF ERR<>30 THEN ON ERROR GOTO
1060 PRINT CHR$(7):PRINT"FEL INSLAGET !"
1070 RESUME
1080 IF ERR<>30 THEN ON ERROR GOTO
1090 PRINT CHR$(7):PRINT"FEL INSLAGET !"
1100 RESUME 770
