10 PRINT TAB(13);"SL[KTFORSKNING"
20 REM
30 REM WRITTEN BY STAN W. MERRIL
40 REM OF COMP RES ASSOCIATES
50 REM CHICAGO.
60 REM [NDRAT F\R FLEX BASIC OCH F\R
70 REM SVENSKA F\RH]LLANDEN AV
80 REM GUNNSR LOVIUS,MARS 1980
90 REM
100 REM ALLOCATE FILE AND COUNT NUMBER OF CASES ALREADY IN IT
110 REM SET COUNTERS
120 OPEN "GEN1" AS 1
130 OPEN "GEN2" AS 2
140 PRINT:PRINT:PRINT
150 ON ERROR GOTO 410
160 DIM B1$(3),B2$(3),B3$(3),B4$(3)
170 DIM C$(3),G$(3),K$(3),D2$(3)
180 REM
190 FIELD #1,8 AS B$,25ASD$,16ASE$,20ASH$,25ASQ$,8ASR$,25 AS S$,8AST$,4ASF$,30ASD1$,16ASJ$,20ASL$,30ASD3$,16 AS C$(1)
200 REM
210 FIELD #2,16ASC$(2),16ASC$(3),25ASG$(1),25ASG$(2),25ASG$(3),20ASK$(1),20ASK$(2),20ASK$(3),30ASD2$(1),30ASD2$(2),25ASD2$(3)
220 LET I2=0
230 LET I3=0: REM I3 COUNTS NUMBER OF CASES IN FILE
240 REM
250 PRINT "SKA JAG LISTA ALLA NAMNEN"
260 PRINT "I FILEN?"
270 INPUT W1$
280 PRINT
290 IF LEFT$(W1$,1)="J" THEN PRINT "FILEN INNEH]LLER F\LJANDE NAMN:"
300 PRINT
310 LET I3=I3+1
320 LET E=I3
330 GET #1,RECORD E
340 GET #2,RECORD E
350 IF LEFT$(W1$,1)="J" THEN PRINT CVT$F(B$),D$
360 REM
370 IF CVT$F(B$)>I2 THEN LET I2=CVT$F(B$)
380 IF CVT$F(R$)>I2 THEN LET I2=CVT$F(R$)
390 IF CVT$F(T$)>I2 THEN LET I2=CVT$F(T$)
400 GOTO 310
410 RESUME 430
420 REM
430 PRINT:PRINT:PRINT
440 PRINT "ANGE NUMRET P] DEN \NSKADE"
450 PRINT "PROCEDUREN:"
460 PRINT
470 PRINT TAB(10);'(1) F\R IN NYA DATA'
480 PRINT TAB(10);'(2) L[S AV ELLER REVIDERA DATA'
490 PRINT TAB(10);'(3) LISTA STAMTR[DET'
500 PRINT TAB(10);'(4) AVSLUTA PROGRAMMET'
510 INPUT A
520 IF A=1 THEN GOSUB 610:REM ENTER INFORMATION
530 IF A=2 THEN GOSUB 2340:REM READ THE INFORMATION
540 IF A=3 THEN GOSUB 3420:REM LISTA STAMTR[DET
550 IF A=4 THEN GOTO 5350:REM END PROGRAM
560 GOTO 430: REM INPUT ERROR - TRY AGAIN
570 REM
580 REM PROCEDURE SUBROUTINE #1
590 REM    BIRTH CERTIFICATE INFORMATION
600 REM
610 PRINT:PRINT
620 PRINT '[NDAM]L: GENETISKA DATA TILL FILEN'
630 PRINT
640 INPUT "ANGE 'RECORDNR'=PERSONS NR I FILEN: ";Y
650 LET E=Y
660 PUT #1,RECORD E
670 PUT #2,RECORD E
680 PRINT:PRINT
690 PRINT "ANGE PERSONENS NAMN"
700 PRINT "(ANGE FLICKNAMNET I TILL[MPL.FALL)"
710 GET #1,RECORD E
720 GET #2,RECORD E
730 INPUT A$
740 LSET D$=A$
750 PRINT:PRINT
760 PRINT "F\R ATT UNDVIKA,"
770 PRINT "ATT ";D$
780 PRINT "F\RV[XLAS MED N]GON ANNAN"
790 PRINT "SOM HAR LIKNANDE NAMN,B\R"
800 PRINT "HAN/HON HA ETT UNIKT NR I DENNA FIL"
810 PRINT
820 PRINT "HAR NUMMER REDAN TILLDELATS?"
830 INPUT V$
840 IF LEFT$(V$,1)="J" THEN 940
850 IF V$<>'N'THEN IF V$<>'NEJ'THEN GOTO 810
860 LET I2=I2+1
870 LET B=I2
880 PRINT
890 PRINT "HAN/HON HAR TILLDELATS NUMMER:"
900 PRINT
910 PRINT TAB(10);B
920 LSET B$=CVTF$(B)
930 GOTO 980
940 PRINT
950 PRINT "VAR SN[LL OCH ANGE NUMRET:"
960 INPUT B
970 LSET B$=CVTF$(B)
980 PRINT:PRINT
990 PRINT "JAG BEH\VER EN DEL DATA"
1000 PRINT "ANGE DATA I F\LJANDE"
1010 PRINT "FORMAT:  DAG  M]NAD  ]R"
1020 PRINT "   T.EX.  23  APRIL 1949"
1030 PRINT "OM DATUM EJ [R BEKANT,SKRIV 'OBEKANT'"
1040 PRINT:PRINT
1050 PRINT "N[R F\DDES ";D$;"(D M ]R)?"
1060 INPUT A1$
1070 LSET E$=A1$
1080 PRINT
1090 PRINT "VAR F\DDES HAN/HON?"
1100 INPUT A2$
1110 LSET H$=A2$
1120 PRINT:PRINT
1130 PRINT "VAD HETTE ";D$;"S FADER?"
1140 PRINT "(OM DU EJ VET, SKRIV 'OBEKANT')"
1150 INPUT A3$
1160 LSET Q$=A3$
1170 IF Q$='OBEKANT' THEN GOTO 1370
1180 PRINT:PRINT
1190 PRINT "HAR ";Q$;" REDAN TILLDELATS NUMMER?"
1200 INPUT V$
1210 IF LEFT$(V$,1)="J" THEN 1320
1220 IF V$<>'N'THEN IF V$<>'NEJ'THEN GOTO 1180
1230 PRINT
1240 LET I2=I2+1
1250 LET R=I2
1260 PRINT "HAN HAR TILLDELATS NUMRET:"
1270 PRINT
1280 PRINT TAB(10);R
1290 LSET R$=CVTF$(R)
1300 PRINT:PRINT
1310 GOTO 1390
1320 PRINT
1330 PRINT "VAR SN[LL, ANGE NUMRET:"
1340 INPUT R
1350 LSET R$=CVTF$(R)
1360 GOTO 1390
1370 LET R=0
1380 PRINT:PRINT
1390 PRINT "VAD HETTE ";D$;"S MOR?"
1400 PRINT "(ANV[ND OM M\JLIGT FLICKNAMNET."
1410 PRINT "VET DU EJ NAMNET,SKRIV 'OBEKANT')"
1420 INPUT A4$
1430 LSET S$=A4$
1440 IF S$='OBEKANT' THEN GOTO 1620
1450 PRINT:PRINT
1460 PRINT "HAR HON REDAN TILLDELATS NUMMER?"
1470 INPUT V$
1480 IF LEFT$(V$,1)="J" THEN GOTO 1580
1490 IF V$<>'N'THEN IF V$<>'NEJ' THEN GOTO 1450
1500 PRINT
1510 LET I2=I2+1
1520 LET T=I2
1530 PRINT "HON HAR TILLDELATS NUMRET:"
1540 PRINT
1550 PRINT TAB(10);T
1560 LSET T$=CVTF$(T)
1570 GOTO 1640:   REM GO BACK FOR NUMBER
1580 PRINT
1590 PRINT "VAR SN[LL, ANGE NUMRET:"
1600 INPUT T
1610 LSET T$=CVTF$(T)
1620 PRINT:PRINT
1630 LET T=0
1640 PRINT:PRINT
1650 PRINT "VARIFR]N FICK DU INFO OM"
1660 PRINT D$;"S F\DELSE OCH H[RKOMST?"
1670 PRINT "SPECIFIERA (MAX 30 TECKEN)"
1680 INPUT A5$
1690 LSET D1$=A5$
1700 REM
1710 REM INFO FR]N [KTENSSKAPSINTYG
1720 REM
1730 PRINT:PRINT
1740 PRINT "HUR M]NGA G]NGER VAR"
1750 PRINT D$;" GIFT?"
1760 PRINT "ANGE ETT TAL MELLAN 0 OCH 3"
1770 INPUT F
1780 IF F=0 THEN 2000
1790 LSET F$=CVTF$(F)
1800 FOR I=1 TO F
1810   PRINT:PRINT
1820   PRINT "ANGE DATUM F\R GIFTERM]LET#";I;"(DAG M]N ]R)"
1830 INPUT B1$(I)
1840 LSET C$(I)=B1$(I)
1850   PRINT
1860   PRINT "ANGE MAKENS (MAKANS) FULLST[NDIGA NAMN"
1870   PRINT "(ANV[ND FLICKNAMNET I TILL[MPL FALL"
1880 INPUT B2$(I)
1890 LSET G$(I)=B2$(I)
1900   PRINT
1910   PRINT "VAR GIFTE DE SIG?"
1920 INPUT B3$(I)
1930 LSET K$(I)=B3$(I)
1940   PRINT:PRINT
1950 PRINT "VAR FICK DU INFO OM GIFTERM]L # ";I; "?"
1960   PRINT "(MAX 30 TECKEN)"
1970    INPUT B4$(I)
1980   LSET D2$(I)=B4$(I)
1990 NEXT I
2000 IF F=0 THEN B1$(I)='OGIFT'
2010 LSET C$(I)=B1$(I)
2020 IF F=0 THEN B2$(I)='OGIFT'
2030 LSET G$(I)=B2$(I)
2040 REM
2050 REM   DEATH CERTIFICATE INFORMATION
2060 REM
2070 PRINT:PRINT
2080 PRINT "N[R DOG ";D$;" (DAG M]N ]R)?"
2090 PRINT "(OM DU EJ VET DET,SKRIV 'OBEKANT'."
2100 PRINT "OM HAN [R FORTFARANDE I LIVET, SKRIV 'LEVER'.)"
2110 INPUT A6$
2120 LSET J$=A6$
2130 IF A6$='LEVER' THEN GOTO 2230
2140 PRINT:PRINT
2150 PRINT "VAR DOG HAN/HON?"
2160 INPUT A7$
2170 LSET L$=A7$
2180 PRINT:PRINT
2190 PRINT "VAR FICK DU INFO OM HANS/HENNES D\D?"
2200 INPUT A8$
2210 LSET D3$=A8$
2220 GOTO 2250
2230 IF A6$='LEVER' THEN A7$='TILL[MPAS EJ'
2240 LSET L$=A7$
2250 PRINT:PRINT:PRINT
2260 REM
2270 PUT #1,RECORD E
2280 PUT #2,RECORD E
2290 RETURN
2300 REM
2310 REM     PROCEDURE FOR SUBROUTINE #2
2320 REM        READ AND REVISE FILE
2330 REM
2340 PRINT:PRINT
2350 PRINT "L[S- OCH REVIDERINGSFILE."
2360 PRINT:PRINT
2370 REM
2380 PRINT "VILL DU S\KA EFTER NAMN ELLER NUMMER?"
2390 INPUT W2$
2400 PRINT
2410 IF W2$='NAMN' THEN GOTO 2550
2420 IF W2$<>'NUMMER' THEN GOTO 2380
2430 PRINT "VAR SN[LL, SKRIV NUMRET"
2440 INPUT U1
2450 PRINT
2460 LET Z5=0
2470 LET Z5=Z5+1
2480 IF Z5>I3 THEN PRINT "PERSON # ";U1;" FINNS EJ I FILEN"
2490 IF Z5>I3 THEN GOTO 3310
2500 LET E=U1
2510 GET #1,RECORD E
2520 GET #2,RECORD E
2530 IF U1<>CVT$F(B$) THEN 2470
2540 GOTO 2700
2550 PRINT "OM DU S\KER N]GON S[RSKILD,"
2560 PRINT "ANGE HANS/HENNES NAMN. OM DU VILL"
2570 PRINT "L[SA IGENOM HELA FILEN,SKRIV 'ALLA'."
2580 INPUT P1$
2590 PRINT
2600 LET P2$=P1$
2610 LET Z5=0
2620 LET Z5=Z5+1
2630 IF Z5+1>I3 THEN PRINT P1$;" FINNS EJ I FILEN."
2640 IF Z5+1>I3 THEN 3310
2650 LET E=Z5
2660 GET #1,RECORD E
2670 GET #2,RECORD E
2680 IF P1$='ALLA' THEN LET P2$=D$
2690 IF P2$<>D$ THEN GOTO 2620
2700 PRINT:PRINT
2710 PRINT "GENEALOGISK INFO F\R:"
2720 PRINT D$
2730 PRINT "NUMMER: ";CVT$F(B$)
2740 PRINT
2750 PRINT "F\DD ";E$
2760 PRINT "F\DELSEORT: ";H$
2770 PRINT
2780 PRINT "FADER: ";Q$;" (NUMMER: ";CVT$F(R$);" )"
2790 PRINT "MODER: ";S$;" (NUMMER: ";CVT$F(T$);" )"
2800 PRINT
2810 LET F=CVT$F(F$)
2820 IF F=0 GOTO 2910
2830 PRINT "GIFT MED:"
2840 FOR I=1 TO F
2850 PRINT '   ';G$(I)
2860 PRINT '      DEN: ';C$(I)
2870 PRINT '      PLATS: ';K$(I)
2880 PRINT
2890 NEXT I
2900 PRINT:PRINT
2910 IF J$='LEVER' THEN GOTO 2940
2920 PRINT "D\DSDATUM: ";J$
2930 PRINT "D\DSORT: ";L$
2940 PRINT:PRINT
2950 PRINT "K[LLUPPGIFT:"
2960 PRINT " F\DELSE,H[RSTAMN.: ";D1$
2970 FOR I=1 TO F
2980 IF F<>0 THEN PRINT " GIFTERM]L #";I;":";D2$(I)
2990 NEXT I
3000 IF J$='LEVER' THEN 3020
3010 PRINT " D\DSFALL: ";D3$
3020 PRINT:PRINT
3030 REM
3040 PRINT "VILL DU [NDRA N]GONTING?"
3050 INPUT P3$
3060 IF LEFT$(P3$,1)="N" THEN GOTO 3290
3070 REM
3080 REM     MENU FOR REVISIONS
3090 REM
3100 PRINT
3110 PRINT "ANGE \NSKAT AVSNITT"
3120 PRINT
3130 PRINT '   1) NAMN,NUMMER'
3140 PRINT '   2) F\DELSE,H[RKOMST'
3150 PRINT '   3) INFO OM GIFTERM]L'
3160 PRINT '   4) INFO OM D\DSFALL'
3170 PRINT '   5) INGEN [NDRING'
3180 PRINT
3190 INPUT P4
3200 IF P4=1 THEN GOSUB 4600
3210 IF P4=2 THEN GOSUB 4720
3220 IF P4=3 THEN GOSUB 4910
3230 IF P4=4 THEN GOSUB 5170
3240 IF P4=5 THEN GOSUB 3310
3250 PRINT
3260 REM
3270 PRINT "VILL DU G\RA N]GON MER [NDRING?"
3280 GOTO 3050
3290 PUT #1,RECORD E
3300 PUT #2,RECORD E
3310 PRINT:PRINT
3320 REM
3330 PRINT "VILL DU SE FLERA UPPL[GG?"
3340 INPUT P$
3350 IF LEFT$(P$,1)="J" THEN IF P1$<>'ALLA' THEN 2370
3360 IF LEFT$(P$,1)="J" THEN 2620
3370 REM
3380 RETURN
3390 REM PROCEDURE SUBROUTINE #3
3400 REM    LINK PEDIGREE BY NUMBER
3410 REM
3420 PRINT:PRINT
3430 PRINT "LISTA STAMTR[DET"
3440 PRINT:PRINT
3450 LET I4=0
3460 PRINT "VEMS STAMTR[D SKA JAG LISTA?"
3470 PRINT "(ANGE NUMRET)"
3480 INPUT X1
3490 PRINT:PRINT
3500 PRINT "'F','M' BETYDER 'FADER' RESP 'MODER'"
3510 PRINT:PRINT
3520 REM
3530 REM FIND PARENTS AND LIST
3540 REM
3550 LET I4=I4+1
3560 IF I4>I3 THEN PRINT "PERSON #";X1;" FINNS INTE I FILEN"
3570 IF I4>I3 THEN 4570
3580 LET E=X1
3590 GET #1,RECORD E
3600 GET #2,RECORD E
3610 IF CVT$F(B$)<>X1 THEN 3550
3620 PRINT TAB(15);"STAMTR[D F\R PERSON #";CVT$F(B$)
3630 PRINT
3640 PRINT D$
3650 PRINT
3660 LET R=CVT$F(R$):LET T=CVT$F(T$)
3670 PRINT TAB(5);'F:';Q$;'(NUMBER:';R;')'
3680 PRINT TAB(5);'M:';S$;'(NUMMER:';T;')'
3690 PRINT
3700 REM
3710 REM  FIND GRANDPARENTS AND LIST
3720 REM
3730 LET X2=R
3740 LET X3=T
3750 LET I5=0
3760 LET IA=0
3770 IF X2<>0 THEN IA=IA+1
3780 IF X3<>0 THEN IA=IA+1
3790 IF IA=0 THEN 4570
3800 LET I4=I4+1
3810 LET E=I4
3820 GET #1,RECORD E
3830 GET #2,RECORD E
3840 IF X2=CVT$F(B$) THEN 3870
3850 IF X3=CVT$F(B$) THEN 3960
3860 GOTO 3800
3870 LET R=CVT$F(R$):LET T=CVT$F(T$)
3880 PRINT TAB(10);'FF:';Q$;'NUMMER:';R;')'
3890 PRINT TAB(10);'FM:';S$;'NUMMER:';T;')'
3900 PRINT
3910 LET X4=R
3920 LET X5=T
3930 LET I5=I5+1
3940 IF I5<IA THEN 3800
3950 IF I5>=IA THEN 4100
3960 LET R=CVT$F(R$):LET T=CVT$F(T$)
3970 PRINT TAB(10);'MF:';Q$;'NUMMER:';R;')'
3980 PRINT TAB(10);'MM:';S$;'NUMMER:';T;')'
3990 PRINT
4000 LET X6=R
4010 LET X7=T
4020 LET I5=I5+1
4030 IF I5<IA THEN 3800
4040 IF I5>=IA THEN 4100
4050 LET IB=0
4060 PRINT
4070 REM
4080 REM  FIND GREAT GRANDPARENTS AND LIST
4090 REM
4100 LET I5=0
4110 LET IA=0
4120 IF X4<>0 THEN IA=IA+1
4130 IF X5<>0 THEN IA=IA+1
4140 IF X6<>0 THEN IA=IA+1
4150 IF X7<>0 THEN IA=IA+1
4160 IF IA=0 THEN 4570
4170 LET I4=I4+1
4180 IF IA>4 THEN PRINT "STAMTR[DET KAN EJ UT\KAS MERA"
4190 IF IA>4 THEN 4570
4200 LET E=I4
4210 GET #1,RECORD E
4220 GET #2,RECORD E
4230 LET B=CVT$F(B$)
4240 IF X4=B THEN 4290
4250 IF X5=B THEN 4360
4260 IF X6=B THEN 4430
4270 IF X7=B THEN 4500
4280 GOTO 4170
4290 LET R=CVT$F(R$):LET T=CVT$F(T$)
4300 PRINT TAB(15);'FFF:';Q$;'(NUMMER:';R;')'
4310 PRINT TAB(15);'FFM:';S$;'(NUMMER:';T;')'
4320 PRINT
4330 LET I5=I5+1
4340 IF I5<IA THEN 4170
4350 IF I5>=IA THEN 4570
4360 LET R=CVT$F(R$):LET T=CVT$F(T$)
4370 PRINT TAB(15);'FMF:';Q$;'(NUMMER:';R;')'
4380 PRINT TAB(15);'FMM:';S$;'(NUMMER:';T;')'
4390 PRINT
4400 LET I5=I5+1
4410 IF I5<IA THEN 4170
4420 IF I5>=IA THEN 4570
4430 LET R=CVT$F(R$):T=CVT$F(T$)
4440 PRINT TAB(15);'MFF:';Q$;'(NUMMER:';R;')'
4450 PRINT TAB(15);'MFM:';S$;'(NUMMER:';T;')'
4460 PRINT
4470 LET I5=I5+1
4480 IF I5<IA THEN 4170
4490 IF I5>=IA THEN4570
4500 LET R=CVT$F(R$):LET T=CVT$F(T$)
4510 PRINT TAB(15);'MMF:';Q$;'(NUMMER:';R;')'
4520 PRINT TAB(15);'MMM:';S$;'(NUMMER:';T;')'
4530 PRINT
4540 LET I5=I5+1
4550 IF I5<IA THEN 4170
4560 REM
4570 RETURN
4580 REM
4590 REM    REVISION SUBROUTINE #1
4600 REM      CHANGE NAME AND NUMBER
4610 REM
4620 PRINT:PRINT
4630 INPUT "NAMN:";A$
4640 LSET D$=A$
4650 PRINT
4660 INPUT "NUMMER: ";B
4670 LSET B$=CVTF$(B)
4680 RETURN
4690 REM    REVISION SUBROUTINE #2
4700 REM      CHANGE BIRTH INFO
4710 REM
4720 REM
4730 PRINT:PRINT
4740 INPUT "F\DELSEDATA: ";A1$
4750 LSET E$=A1$
4760 INPUT "F\DELSEORT: ";A2
4770 LSET H$=A2$
4780 INPUT "FADERNS NAMN: ";A3$
4790 LSET Q$=A3$
4800 INPUT "FADERNS NUMMER: ";R
4810 LSET R$=CVTF$(R)
4820 INPUT "MODERNS NAMN* ";A4$
4830 LSET S$=A4$
4840 INPUT "MODERNS NUMMER: ";T
4850 LSET T$=CVTF$(T)
4860 INPUT "INFORMATIONSK[LLA: ";A5$
4870 LSET D1$=A5$
4880 RETURN
4890 REM
4900 REM   REVISION SUBROUTINE #3
4910 REM     CHANGE MARRIAGE INFO
4920 REM
4930 PRINT:PRINT
4940 INPUT "VILKET [KTENSKAP (ANGE NR) ?";I
4950 PRINT
4960 PRINT "DATUM F\R GIFTERM[L #";I;
4970 INPUT B1$(I)
4980 LSET C$(I)=B1$(I)
4990 PRINT
5000 INPUT "MAKENS (MAKANS) NAMN";B2$(I)
5010 LSET G$(I)=B2$(I)
5020 PRINT
5030 INPUT "PLATS F\R GIFTERM]L";B3$(I)
5040 LSET K$(I)=B3$(I)
5050 PRINT
5060 PRINT "INFORMATIONSK[LLA?"
5070 INPUT B4$(I)
5080 LSET D2$(I)=B4$(I)
5090 PRINT
5100 PRINT "HUR M]NGA AV DENNE PERSONS"
5110 PRINT "[KTENSKAP GER DENNA FIL INFO OM?"
5120 INPUT F
5130 LSET F$=CVTF$(F)
5140 RETURN
5150 REM
5160 REM   REVISION SUBROUTINE #4
5170 REM     CHANGE DEATH INFO
5180 REM
5190 PRINT:PRINT
5200 PRINT "D\DSDATUM"
5210 PRINT "(OM EJ K[NT, SKRIV 'OBEKANT'. OM"
5220 PRINT "VID LIV, SKRIV 'LEVER'.)"
5230 INPUT A6$
5240 LSET J$=A6$
5250 IF J$='LEVER' GOTO 5310
5260 INPUT "D\DSPLATS: ";A7$
5270 LSET L$=A7$
5280 PRINT "INFORMATIONSK[LLA?"
5290 INPUT A8$
5300 LSET D3$=A8$
5310 RETURN
5320 REM
5330 REM   PROC. SUBROUTINE #4
5340 REM     END OF PROGRAM
5350 CLOSE 1,2
5360 END
