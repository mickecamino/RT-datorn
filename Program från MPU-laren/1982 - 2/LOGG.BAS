3 REM LOGGBOK
5 REM UR MPU-LAREN Nr.2 1982
10 G1=38:G=240:DIM A$(G,1):WD=PEEK(52228):POKE 52228,0
20 REM H[MTAR DATUM FR]N FLEX
30 YY=PEEK(52240):MM=PEEK(52238):DD=PEEK(52239)
40 YY$=MID$(STR$(YY),2,LEN(STR$(YY))-2):IF LEN(YY$)=1 THEN YY$="0"+YY$
50 MM$=MID$(STR$(MM),2,LEN(STR$(MM))-2):IF LEN(MM$)=1 THEN MM$="0"+MM$
60 DD$=MID$(STR$(DD),2,LEN(STR$(DD))-2):IF LEN(DD$)=1 THEN DD$="0"+DD$
70 SS$=YY$+MM$+DD$:X1$="DATUM : bGMT/sGMT sRST/mRST BAND MODE"
80 X2$="==================================":L$="*****************************************"
90 REM RITAR MENY
110 PRINT TAB(10);"A M A T \ R - R A D I O"
120 PRINT:PRINT TAB(3);L$
125 PRINT
130 PRINT TAB(6);"1.";"SKRIVA IN QSO'n'";TAB(27);" DATUM.  ";:PRINT SS$
140 PRINT:PRINTTAB(6);"2.";"S\KA ANROPSSIGNAL";"      ";G-J;"ST"
145 PRINT
150 PRINT TAB(6);"3.";"LAGRA QSO'n'"; TAB(30);"QSO'n'";
160 PRINT "RYMS.":PRINT:PRINT TAB(6);"4.";"H[MTA QSO'n'"
170 PRINT:PRINT TAB(6);"5.";"SORTERA-UTLISTA"
180 PRINT:PRINT TAB(6);"6.";"REDIGERA"
190 PRINT:PRINT TAB(6);"7.";"[NDRA DATUM":PRINT
200 PRINT TAB(3);L$;
215 PRINT:PRINT
230 ON ERROR GOTO 1290
240 PRINT "    V[LJ EN SIFFRA   (1-7)   ";:F=ASC(INCH$(0))
250 IF F<49 ORF>55 THEN 240 ELSEON F-48 GOSUB 280,440,490,600,750,910,1250
260 GOTO 90
270 REM INMATNING
280 PRINT "  INMATNING AV QSO-data "
290 PRINT "###########################";:IF J<0 THEN J=0
300 U2$="":PRINT"  Nr:";J+1:PRINT"  ANROPSSIGNAL   10:";:INPUTLINE V$
310 IF V$="" THEN RETURN ELSE IF LEN(V$)>10 THEN GOSUB 1220:GOTO 300
320 J=J+1:IF J>G THEN 1230 ELSE U2$=V$
330 U1$="":PRINT"  GMT b|rja/slut 9:";:INPUT V$:IF LEN(V$)>9 THEN GOSUB 1220:GOTO 330
340 U1$=":"+SS$+":  "+V$
350 PRINT" RST s{nt/mott  7:";:INPUT V$:IF LEN(V$)>7 THEN GOSUB 1220:GOTO350
360 U1$=U1$+":"+V$
370 PRINT" BAND      4:";:INPUT V$:IF LEN(V$)>4 THEN GOSUB 1220:GOTO 370
380 U1$=U1$+":"+V$
390 PRINT" MODE      6:";:INPUT V$:IF LEN(V$)>6 THEN GOSUB 1220:GOTO 390
400 U1$=U1$+":"+V$
410 PRINT" NAMN-QTH  27:";:INPUT V$:IF LEN(V$)>27 THEN GOSUB 1220:GOTO 410
420 U2$=U2$+":"+V$:A$(J,0)=U2$:A$(J,1)=U1$:GOTO 290
430 REM S\KER I MINNET
440 PRINT TAB(12);"Vad vill du s|ka:":PRINTTAB(7);"ex.Namn,Datum,Signal osv.";:INPUTLINE V$
450 IF V$="" THEN 90 ELSE PRINT;X1$:PRINT X2$:P1=0:FOR R%=1 TO J:FOR K%=0 TO1
460 P=INSTR(1,A$(R%,K%),LEFT$(V$,LEN(V$))):IF P=0 THEN 480
470 PRINT:PRINT"Nr";R%:FOR S%=0 TO 1:PRINT A$(R%,S%):NEXT S%:P1=1:IF ASC(INCH$(0))=13 THEN RETURN
480 NEXT K%:NEXT R%:IF P1 THEN 440 ELSE PRINT:PRINT"Finns ej i loggen":GOTO 440
490 REM LADDAR NER P] DISK
500 ON ERROR GOTO 570
510 PRINT:PRINT TAB(8);"VAD SKALL FILEN HA F\R NAMN ?.":PRINT:PRINT TAB(8);"OBS!!.MAX ]TTA TECKEN ";
520 INPUTLINE F$:IF LEN(F$)>8 THEN GOSUB 1220:GOTO 510
530 IF F$="" THEN 90
540 OPEN NEW F$ AS 1
550 FOR R%=1 TO J:FOR K%=0TO 1:PRINT #1,A$(R%,K%):NEXT K%:NEXT R%
560 CLOSE 1:RETURN
570 IF ERR=7 THEN PRINT CHR$(7):PRINT TAB(8);"DISKETTEN [R FULL":PRINT
580 IF ERR=21 THEN PRINT CHR$(7):PRINT TAB(8);"FELAKTIGT FILNAMN!!":CLOSE 1:RESUME 510
590 PRINT TAB(8);"TRYCK P] RETURN";:Q$=INCH$(0):RESUME 560
600 REM H[MTER DATA
610 ON ERROR GOTO 710:PRINT CHR$(27);CHR$(5)
620 PRINT:PRINT TAB(5);"VILKEN FIL VILL DU H[MTA";:INPUT LINE F$:IF LEN(F$)>8 THEN GOSUB 1220:GOTO 620
630 IF F$="" THEN 90 ELSE J1=J
640 OPEN OLD F$ AS 1
650 PRINT:PRINT TAB(5);"T\MMA BEFINTLIGT INNEH]LL (J/N)?";:Q$=INCH$(0):PRINT
660 IF Q$="J" THEN PRINT:PRINT TAB(5);"[R DU S[KER ?";:Q$=INCH$(0):PRINT:IF Q$="J" THEN J=0:GOTO 680
670 IF Q$<>"N" THEN 650
680 FOR R%=J+1 TO G:J=J+1:IF J>G THEN CLOSE 1:GOTO 1230
690 FOR K%=0 TO 1:INPUT #1, A$(R%,K%):NEXT K%:NEXT R%
700 CLOSE 1:RETURN
710 IF ERR=8 THEN J=J-1:RESUME 700
720 IF ERR=4 THEN PRINT:PRINT TAB(5);"DEN FILEN FINNS EJ";CHR$(7):CLOSE 1:J=J1:RESUME 620
730 IF ERR=21 THEN PRINT:PRINT TAB(5);"FELAKTIGT FILNAMN!!":CLOSE 1:RESUME 620
740 RESUME 700
750 REM SORTERA UTLISTA
755 PRINT
760 PRINT"     S = Sortera ---- U = Utlista   ";
770 G$=INCH$(0):IF G$="S" THEN 780 ELSE IF G$="U" THEN 820 ELSE 240
780 PRINT" SORTERING P]G]R (V[NTA)!!"
790 FOR I%=1 TO J-1:FOR R%=I%+1 TO J:IF A$(I%,1)<A$(R%,1) THEN 810
800 FOR K%=0 TO 1:SWAP A$(R%,K%),A$(I%,K%):NEXT K%
810 NEXT R%:NEXT I%:RETURN
820 ON ERROR GOTO 1300
825 PRINT
830 PRINT "     Printer/Terminal (P/T)   ";:Q$=INCH$(0)
840 IF Q$="P" THEN OPEN"0.P.CMD"AS0
850 PRINT#0:PRINT#0,X1$:PRINT#0,X2$
860 FOR R=1 TO J:IF R=1 THEN 880
870 IF Q$="T" THEN S$=STR$(R):IF RIGHT$(S$,2)="1" OR RIGHT$(S$,2)="6" THEN IF ASC(INCH$(0))=13 THEN RETURN
880 PRINT #0:PRINT #0,"NR.";R:FOR K=0 TO 1:PRINT #0,A$(R,K)
890 NEXT K:NEXT R:CLOSE 0:PRINT
900 IF Q$="P" THEN RETURN ELSE PRINT TAB(12);"TRYCK P] RETURN";:Q$=INCH$(0):RETURN
910 REM REDIGERA
920 PRINT CHR$(27);CHR$(5):PRINT TAB(14);"REDIGERING":IF J>1 THEN E$="E" ELSE E$=""
930 PRINT TAB(10);"DET FINNS";J;"LOGGAD";E$
940 PRINT:PRINT:PRINT TAB(6);"Vilket Nr. vill du redigera";:INPUTLINE V$
950 IF VAL(V$)> J THEN PRINT:PRINT:PRINT TAB(6)"DET FINNS INTE S] M]NGA LOGGADE!";CHR$(7);:GOTO 940
960 IF V$="" THEN RETURN
970 PRINT
980 R=VAL(V$):IF R<1 OR R>J THEN 920
990 FOR K=0 TO 1:PRINT:PRINT A$(R,K);:NEXT K:PRINT
1000 FOR K=0 TO 1:PRINT:GOSUB 1060:IF K<>0 THEN 1020
1010 IF V$="BORT" THEN 1040
1020 IF ASC(V$)=0 THEN PRINT A$(R,K) ELSE A$(R,K)=V$
1030 NEXT K:GOTO 920
1040 J=J-1:FOR R=R TO J:FOR K=0 TO 1:A$(R,K)=A$(R+1,K)
1050 NEXT K:NEXT R:GOTO 920
1060 PRINT A$(R,K):PRINT X2$:V$=A$(0,0):G$="":G2=LEN(A$(R,K)):A=0
1070 G$=INCH$(0)
1080 IF ASC(G$)=13 THEN 1190
1090 IF ASC(G$)<>2 THEN 1130
1100 IF ASC(G$)=2 THEN G$=CHR$(8)
1110 IF LEN(V$)=0 THEN 1070
1120 PRINT G$;" ";G$;:V$=LEFT$(V$,LEN(V$)-1):GOTO 1070
1130 IF ASC(G$)<>6 THEN 1180
1140 IF A+1>G2 THEN 1070
1150 A=A+1
1160 G$=MID$(A$(R,K),A,1)
1170 PRINT G$;
1180 V$=V$+G$:IF LEN(V$)>G1 THEN GOSUB 1220 ELSE 1070
1190 PRINT:IF LEN(V$)<=G1 THEN RETURN
1200 GOTO 1060
1210 REM SUBRUTINER
1220 PRINT:PRINT CHR$(7);TAB(4);"F|r l}ng text!":PRINT:RETURN
1230 PRINT CHR$(7):PRINT TAB(5);"MINNET [R FULLT.TRYCK P] RETURN";:G$=INCH$(0):J=G:RETURN
1240 REM DATUM[NDRING
1250 PRINT CHR$(27);CHR$(5);"      AKTUELLT DATUM [R ";SS$
1260 PRINT:PRINT TAB(4);"NYTT ELLER DAGENS DATUM (N/D)";:F=ASC(INCH$(0)):IF F=13 THEN RETURN ELSE IF F=68 THEN 30
1270 IF F=78 THEN PRINT:PRINT:PRINT TAB(4);"NYTT DATUM (]]MMDD)":INPUT SS$:IF LEN(SS$)>6THEN GOSUB 1220:GOTO 1270
1280 IF F=78 THEN RETURN ELSE 1260
1290 REM OMSTART VID ERROR
1300 IF ERR=34 THEN POKE 52228,WD:STOP
1310 RESUME
