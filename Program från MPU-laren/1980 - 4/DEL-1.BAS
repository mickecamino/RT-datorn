10 REM ********************************
15 REM *      ADRESS-REGISTER         *
16 REM *      f|r max 320 namn        *
18 REM *      1 namn per sektor       *
20 REM * av ]ke Sten{ng 80-07-15 f|r  *
25 REM * TSC basic, minifloppy, CT-82 *
30 REM ********************************
31 REM *       DEL-1.BAS PROGRAM      *
32 REM ********************************
35 PRINT CHR$(28);CHR$(18)
40 PRINT CHR$(29);CHR$(29);CHR$(31)
45 PRINT CHR$(30);CHR$(30)
50 POKE HEX("AC03"),0
55 REM DATUMSTR[NG ]]-MM-DD
60 YY=PEEK(HEX("AC10")):MM=PEEK(HEX("AC0E")):DD=PEEK(HEX("AC0F"))
62 YY$=MID$(STR$(YY),2,LEN(STR$(YY))-2)
64 IF LEN(YY$)=1 THEN YY$="0"+YY$
66 MM$=MID$(STR$(MM),2,LEN(STR$(MM))-2)
68 IF LEN(MM$)=1 THEN MM$="0"+MM$
70 DD$=MID$(STR$(DD),2,LEN(STR$(DD))-2)
72 IF LEN(DD$)=1 THEN DD$="0"+DD$
75 SS$=YY$+"-"+MM$+"-"+DD$: REM DATUMSTR[NG=SS$
80 DIM A1$(30),B1$(30),C1$(30),D1$(30),E1$(30),F1$(30),G1$(30),S1$(30)
85 DA$="REG-1":    REM DATAFILENS NAMN
86 FA$="MPU-laren":REM FIRMANS NAMN
87 AN$="]. STEN[NG, BANKERYD":AN=LEN(AN$): REM  ANV[NDARENS NAMN ELL FIRMA
90 ON ERROR GOTO 300
100 OPEN OLD DA$ AS1
120 FIELD #1,4ASA$,30ASB$,20ASC$,30ASD$,25ASE$,25ASF$,13ASG$,87ASZ$,8ASS$,3ASTR$,3ASBO$,3ASSE$,1ASCR$
135 PRINT"* * ADRESSREGISTER (MAX 320 NAMN) F\R ";FA$;" * *":PRINT:PRINT
150 PRINT"* * DEL - PROGRAM * *"
155 GET#1,RECORD1:TR=VAL(TR$):BO=VAL(BO$):SE=VAL(SE$):KA=TR-BO
160 GOTO 520
300 IF ERR<>4 THEN ON ERROR GOTO
310 PRINTCHR$(7):PRINT"DET FINNS INGEN DATAFIL! G] TILL HUVUDPROGRAMMET!"
320 RESUME 525
520 REM * *  MENY  * *
522 PRINT:PRINT"REGISTRET INNEH]LLER";KA;"KUNDER."
525 PRINT:PRINT
530 ON ERROR GOTO 11000
540 PRINT TAB(5);"V[LJ:  (alla printer ell sk{rm)"
550 PRINT TAB(5);"1. EN KUND I TAGET"
560 PRINT TAB(5);"2. LISTNING, VERTIKALT"
570 PRINT TAB(5);"3. LISTNING, ett namn/tv} rader + F:a o sidnumrering"
580 PRINT TAB(5);"4. --"
590 PRINT TAB(5);"5. --"
600 PRINT TAB(5);"6. TILL HUVUDPROGRAM"
610 PRINT TAB(5);"7. SLUT":PRINT
630 INPUT "V[LJ NR";H
632 IF KA=0 AND H<4 THENPRINTCHR$(12):PRINTCHR$(7):PRINT"REGISTRET [R TOMT! NR";H;"G]R D] EJ ATT ANV[NDA!":GOTO520
635 IF H<1 OR H>7 THEN PRINT CHR$(7):PRINT"FEL INLSLAGET!":GOTO 630
640 ON H GOTO 1000,2000,3000,4000,5000,6000,10000
1000 PRINTCHR$(12):PRINT"* * EN KUND I TAGET * *":PRINT
1005 ON ERROR GOTO 11000
1020 PRINT "ANGE:   1. SK[RM"
1030 PRINT "        2. PRINTER"
1040 INPUT "NR";G
1045 IF G=1 THEN 1080
1050 IF G=2 THEN OPEN"0.PRINT"AS0:GOTO 1075
1070 PRINT CHR$(7):PRINT"FEL INSLAGET!":GOTO 1040
1075 PRINTCHR$(7):PRINT"[R PRINTERN KLAR - TRYCK RETURN";:INPUTLINEAB$
1080 ON ERROR GOTO 11000
1085 PRINT:INPUT"ANGE KUNDNUMRET ";A
1086 IF A<=1000 THENPRINTCHR$(7):PRINT"OBS! KUNDNUMRET SKALL VARA \VER 1000!":GOTO1085
1090 PRINT
1140 Z5=A-1000:E=Z5
1145 IF Z5>TR THENPRINTCHR$(7):PRINT"KUNDUMMER";A;"FINNS EJ!":GOTO520
1150 GET#1,RECORD E
1153 IF B$="" THENPRINTCHR$(7):PRINT"NR";A;"[R BORTTAGEN!":GOTO520
1155 IFG=1THENPRINTA$;TAB(7);S$
1160 PRINT#0,B$:PRINT#0,C$:PRINT#0,D$:PRINT#0,E$;" ";F$
1190 IFG=1 THENPRINT G$
1210 IF G=2 THEN CLOSE 0
1220 GOTO 520
2000 PRINTCHR$(12):PRINT"* * LISTNING VERTIKALT * *":PRINT
2020 ON ERROR GOTO 11000
2040 PRINT "ANGE:   1. SK[RM"
2050 PRINT "        2. PRINTER"
2060 INPUT "NR";G
2070 IF G=1 THENPRINT:PRINTCHR$(29);CHR$(29);CHR$(17):GOTO2300
2080 IF G=2 THEN OPEN"0.PRINT"AS0:GOTO 2100
2090 PRINT CHR$(7):PRINT"FEL INSLAGET!":GOTO 2060
2100 REM F\R OLIKA VAL VID PRINTER
2110 PRINTCHR$(7):PRINT"[R PRINTERN KLAR - TRYCK RETURN";:INPUTLINEAB$
2300 REM DISK TILL MINNE
2310 C=0:Q=0:B=0:Y=0:Z5=0
2330 D=30:REM ANTAL NAMN
2335 Q=Q+D
2340 IF Q>KA THEN Q1=Q-KA:D=D-Q1
2345 FOR B=1 TO D
2350 Z5=Z5+1:E=Z5
2355 GET#1,RECORD E
2360 IF B$="" THEN2350
2365 Y=B
2370 A1$(Y)=A$:B1$(Y)=B$:C1$(Y)=C$:D1$(Y)=D$:E1$(Y)=E$:F1$(Y)=F$:G1$(Y)=G$:S1$(Y)=S$
2390 NEXT B
2400 REM TILL SK[RM ELL PRINTER
2450 FOR Y=1 TO D
2455 C=C+1
2457 IF G=1 THENPRINTA1$(Y);TAB(7);S1$(Y)
2460 IF G=1THENPRINTB1$(Y):PRINTC1$(Y)
2550 IF G=2THENPRINT#0,B1$(Y);TAB(35);A1$(Y):PRINT#0,C1$(Y)
2570 PRINT#0,D1$(Y):PRINT#0,E1$(Y);" ";F1$(Y)
2572 IFG=1 THENPRINT G1$(Y)
2575 PRINT#0:IFG=2 THEN PRINT#0
2670 NEXT Y
2700 IF C=KA THEN 2880
2710 GOTO 2330
2880 IF G=2 THEN CLOSE 0
2882 IF G=1 THENPRINTCHR$(29);CHR$(29);CHR$(31)
2885 GOTO 520
3000 PRINTCHR$(12):PRINT"* * LISTNING, ett namn/tv} rader + F:a o sidnumrering * *":PRINT
3020 ON ERROR GOTO 11000
3040 PRINT"ANGE:   1. SK[RM"
3050 PRINT"        2. PRINTER"
3060 INPUT"NR";G
3070 IF G=1 THENPRINTCHR$(29);CHR$(29);CHR$(17):GOTO3300
3080 IF G=2 THEN OPEN"0.PRINT"AS0:GOTO3100
3090 PRINT CHR$(7):PRINT"FEL INSLAGET!":GOTO3060
3100 REM PRINTER H[R
3110 PRINT:PRINTCHR$(7):PRINT"ST[LL IN PRINTERN P] 14 TECKEN/TUM - TRYCK RETURN";:INPUTLINEAB$
3300 REM DISK TILL MINNE
3310 C=0:Q=0:Q1=0:B=0:Y=0:Z5=0:SI=0
3315 T1=88:T3=31:T4=62:T5=88:REM TAB I UTMATNING
3330 D=30:REM ANTAL NAMN TILL MINNE
3335 Q=Q+D
3340 IF Q>KA THEN Q1=Q-KA:D=D-Q1
3345 FOR B=1 TO D
3350 Z5=Z5+1:E=Z5
3355 GET#1,RECORDE
3360 IF B$="" THEN 3350
3365 Y=B
3370 A1$(Y)=A$:B1$(Y)=B$:C1$(Y)=C$:D1$(Y)=D$:E1$(Y)=E$:F1$(Y)=F$:G1$(Y)=G$:S1$(Y)=S$
3390 NEXT B
3400 REM TILL PRINTER ELL SK[RM
3402 SI=SI+1
3404 PRINT#0,"ADRESSREGISTER F\R ";FA$;"  ";SS$;"  REGISTRET=";KA;"KUNDER I KUNDNUMMERORDNING";TAB(T1);"SID";SI
3406 FOR U=1TO105:PRINT#0,"-";
3407 NEXT U:PRINT#0
3408 PRINT#0,"NAMN";TAB(T3);"GATA ELL BOX";TAB(T4);"POSTADRESS";TAB(T5);"C/O"
3410 PRINT#0,"KUNDNR";TAB(T3);"DATUM";TAB(T4);"TEL NR";TAB(T5);"LAND"
3415 FOR U=1TO105:PRINT#0,"-";
3416 NEXT U:PRINT#0
3450 FOR Y=1 TO D
3455 C=C+1
3460 PRINT#0,B1$(Y);TAB(T3);D1$(Y);TAB(T4);E1$(Y);TAB(T5);C1$(Y)
3465 PRINT#0," ";A1$(Y);TAB(T3);S1$(Y);TAB(T4);G1$(Y);TAB(T5);F1$(Y)
3470 NEXT Y
3700 IF C=KA THEN FOR U=1 TO AN:PRINT#0,"-";:NEXTU:PRINT#0:PRINT#0,AN$:GOTO3880
3710 FOR U=1 TO 7:PRINT#0:NEXT U:GOTO 3330
3880 IF G=2 THEN CLOSE 0
3882 IF G=1 THENPRINTCHR$(29);CHR$(29);CHR$(31)
3885 GOTO 520
4000 PRINT"FINNS INGET P] 4000!":GOTO520
5000 PRINT"FINNS INGET P] 5000!":GOTO520
6000 CLOSE 1:CHAIN "HUVUD-1.BAS"
10000 PRINT:PRINT "SLUT !":CLOSE 1
10010 PRINT CHR$(29);CHR$(29);CHR$(17):END
11000 REM * OM EN BOKSTAV SKRIVS I EN NUMERISK VARIABEL *
11005 IF ERR<>30 THEN ON ERROR GOTO
11010 PRINT CHR$(7):PRINT"FEL INSLAGET!"
11015 RESUME
