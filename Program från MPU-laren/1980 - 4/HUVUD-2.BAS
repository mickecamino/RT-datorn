10 REM ********************************
15 REM *      ADRESS-REGISTER         *
16 REM *      f|r max 640 namn        *
18 REM *      2 namn per sektor       *
20 REM * av ]ke Sten{ng 80-09-15 f|r  *
25 REM * TSC basic, minifloppy, CT-82 *
30 REM ********************************
31 REM *     HUVUD-2.BAS PROGRAM      *
32 REM ********************************
35 PRINT CHR$(28);CHR$(18)
40 PRINT CHR$(29);CHR$(29);CHR$(31)
45 PRINT CHR$(30);CHR$(30)
50 POKE HEX("AC03"),0
80 DIM A1$(27),B1$(27),C1$(27),D1$(27),E1$(27),G1$(27)
85 DA$="REG-2":    REM DATAFILENS NAMN
86 FA$="MPU-laren":REM FIRMANS NAMN
90 ON ERROR GOTO 300
100 OPEN OLD DA$ AS1:GOTO135
120 FIELD #1,(S-1)*126ASZ$,4ASA$,30ASB$,20ASC$,30ASD$,25ASE$,13ASG$,3ASRB$,1ASCR$:RETURN
135 PRINT"* * ADRESSREGISTER (MAX 640 NAMN) F\R ";FA$;" * *":PRINT:PRINT
136 PRINT"* * HUVUD - PROGRAM * *":PRINT
140 S=1:GOSUB120:GET#1,RECORD1:TR=VAL(RB$)
145 S=2:GOSUB120:GET#1,RECORD1:BO=VAL(RB$):KA=TR-BO
150 S=1:GOSUB120:GET#1,RECORD2:SE=VAL(RB$)
153 IF TR/2+4=SE THEN UT=1
155 PRINT DA$;".DAT FILEN INNEH]LLER";SE;"SEKTORER."
160 PRINT"ADRESSREGISTRET  INNEH]LLER";KA;"NAMN.":PRINT
161 PRINT"PROGRAMMET ANV[NDER TV] NAMN PER SEKTOR.":PRINT
162 IF TR=640 THENPRINTCHR$(7):PRINT"OBS! DATAFILEN SLUT! ANTAL RECORDS = 640!":GOTO520
164 IF SE<=TR/2+10 THENPRINTCHR$(7):PRINT"OBS! DATAFILEN B\R EVENTUELLT UT\KAS!":PRINT
165 PRINT"\NSKAS DATA-FILEN UT\KAD?  J/N  ";:INPUTLINEAB$
170 IF LEFT$(AB$,1)="J" THEN 190
175 IF LEFT$(AB$,1)="N" THENPRINTCHR$(12):GOTO520
180 IFLEFT$(AB$,1)<>"J"ORLEFT$(AB$,1)<>"N" THENPRINTCHR$(7):GOTO165
190 PRINT:INPUT"HUR M]NGA SEKTORER SKALL DATA-FILEN UT\KAS MED ";Z4:Z5=Z4+SE
193 IF Z5>320 THEN Z5=320
195 E=Z5:PUT#1,RECORD E:SE=Z5+6:UT=0
200 S=1:GOSUB120:GET#1,RECORD2:LSETRB$=MID$(STR$(SE),2,LEN(STR$(SE))-2):PUT#1,RECORD2
210 PRINTCHR$(12):GOTO520
300 IF ERR<>4 THEN ON ERROR GOTO
305 CLOSE1:OPEN DA$ AS1
310 PUT#1,RECORD4:SE=10
315 S=1:GOSUB120:GET#1,RECORD2:LSETRB$=MID$(STR$(SE),2,LEN(STR$(SE))-2):PUT#1,RECORD2
320 RESUME 140
400 PRINTCHR$(12):PRINTCHR$(7):PRINT"REGISTRET [R TOMT! NR";H;"G]R D] EJ ATT ANV[NDA!"
520 REM * *  MENY  * *
522 PRINT:PRINT"REGISTRET INNEH]LLER";KA;"KUNDER."
525 PRINT:PRINT
530 ON ERROR GOTO 11000
540 PRINT TAB(5);"V[LJ:"
550 PRINT TAB(5);"1. EN KUND I TAGET      (sk{rm)"
560 PRINT TAB(5);"2. LISTNING, VERTIKALT  (sk{rm)"
570 PRINT TAB(5);"3. INMATNING AV NYA NAMN"
580 PRINT TAB(5);"4. BORTTAGNING"
590 PRINT TAB(5);"5. [NDRING"
600 PRINT TAB(5);"6. TILL DELPROGRAM MED DIV LISTNINGAR  (printer ell sk{rm)"
610 PRINT TAB(5);"7. SLUT":PRINT
630 INPUT "V[LJ NR";H
632 IF H>0 AND H<3 THEN IF KA=0 THEN 400
633 IF H>3 AND H<7 THEN IF KA=0 THEN 400
635 IF H<1 OR H>7 THEN PRINT CHR$(7):PRINT"FEL INLSLAGET!":GOTO 630
640 ON H GOTO 1000,2000,3000,4000,5000,6000,10000
1000 PRINTCHR$(12):PRINT"* * EN KUND I TAGET * *":PRINT
1080 ON ERROR GOTO 11000
1085 PRINT:INPUT"ANGE KUNDNUMRET ";A
1086 IF A<=1000 THENPRINTCHR$(7):PRINT"OBS! KUNDNUMRET SKALL VARA \VER 1000!":GOTO1085
1090 PRINT
1100 Z5=A-1000
1105 IF Z5>TR THENPRINTCHR$(7):PRINT"KUNDNUMMER";A;"FINNS EJ!":GOTO520
1110 IF Z5/2<>INT(Z5/2) THEN S=1:Z5=(Z5+1)/2:E=Z5:GOTO1140
1120 S=2:Z5=Z5/2:E=Z5
1140 GOSUB120:GET#1,RECORDE
1150 IF B$="" THENPRINTCHR$(7):PRINT"NR";A;"[R BORTTAGEN!":GOTO520
1155 PRINT A$
1160 PRINTB$:PRINTC$:PRINTD$:PRINTE$:PRINTG$:GOTO520
2000 PRINTCHR$(12):PRINT"* * LISTNING (vertikalt) * *":PRINT
2040 PRINT CHR$(29);CHR$(29);CHR$(17)
2300 REM DISK TILL MINNE
2310 C=0:Q=0:B=0:Y=0:Z5=1:E=Z5:S=1
2330 D=20:REM ANTAL NAMN
2335 Q=Q+D
2340 IF Q>KA THEN Q1=Q-KA:D=D-Q1
2345 FOR B=1 TO D
2350 GOSUB120:GET#1,RECORDE
2355 IF S=1 THEN S=2:GOTO2360
2356 S=1:Z5=Z5+1:E=Z5
2360 IF B$="" THEN2350
2365 Y=B
2370 A1$(Y)=A$:B1$(Y)=B$:C1$(Y)=C$:D1$(Y)=D$:E1$(Y)=E$:G1$(Y)=G$
2390 NEXT B
2400 REM NU TILL SK[RM
2450 FOR Y=1 TO D
2455 C=C+1
2460 PRINTA1$(Y):PRINTB1$(Y):PRINTC1$(Y):PRINTD1$(Y):PRINTE1$(Y):PRINTG1$(Y)
2465 PRINT
2480 NEXT Y
2700 IF C=KA THEN PRINTCHR$(29);CHR$(29);CHR$(31):GOTO 520
2710 GOTO 2330
3000 REM * INMATNING *
3002 IF BO>0 THEN 3010
3005 IF TR=640 THEN 162
3007 IF UT=1 THEN 3212
3010 PRINT CHR$(12):A=1000:C=0
3020 IF TR=0 THEN S=1:Z5=1:E=Z5:A=A+1:GOTO3100
3022 IF BO>0 THEN 3030
3025 IF TR/2<>INT(TR/2) THEN S=2:Z5=(TR+1)/2:E=Z5:A=1000+TR+1:GOTO3100
3026 S=1:Z5=TR/2+1:E=Z5:A=1000+TR+1:GOTO3100
3030 Z5=0:S=1
3032 Z5=Z5+1:E=Z5
3033 PRINT:PRINT"LETAR EFTER BORTTAGNING(AR)!"
3035 GOSUB120:A=A+1
3040 GET #1,RECORD E
3045 IF B$<>"" THEN IF S=1 THEN S=2:GOTO3035
3046 IF B$<>"" THEN S=1:Z5=Z5+1:E=Z5:GOTO3035
3050 BO=BO-1:GOSUB 3300
3062 PRINT:PRINT"NU F\RS 1 NAMN \VER TILL DISK (F.D. VAKANT NUMMER)!":FORW=1TO3000:NEXTW
3065 LSETB$=BB$:LSETC$=CC$:LSETD$=DD$:LSETE$=EE$:LSETG$=GG$:LSETRB$=""
3070 PUT#1,RECORDE:KA=KA+1
3075 IF AB$="S" THEN 3200
3080 IF BO>0 THEN IF S=1 THEN S=2:GOTO3033
3085 IF BO>0 THEN S=1:GOTO3032
3090 GOTO3025
3100 REM TILL MINNE 20 NAMN
3102 IF TR/2+5>SE THEN 3200
3105 FOR I=1 TO 20
3115 C=C+1:GOSUB 3300
3120 AA$=MID$(STR$(A),2,LEN(STR$(A))-2)
3125 A1$(I)=AA$:B1$(I)=BB$:C1$(I)=CC$:D1$(I)=DD$:E1$(I)=EE$:G1$(I)=GG$
3130 KA=KA+1:TR=TR+1
3132 IF TR=640 THENPRINTCHR$(7):PRINT"OBS! DATAFILEN SLUT! ANTAL RECORDS = 640!":GOTO3150
3133 IF TR/2+4=SE THEN UT=1:GOTO3150
3135 IF AB$="S" THEN 3150
3140 A=A+1
3145 NEXT I
3150 PRINT:PRINT"NU F\RS";C;"NAMN \VER TILL DISK!"
3160 FOR I=1 TO C
3161 FIELD#1,(S-1)*126ASZ$,4ASA$,30ASB$,20ASC$,30ASD$,25ASE$,13ASG$,3ASRB$,1ASCR$
3165 GET #1,RECORD E
3170 LSETA$=A1$(I):LSETB$=B1$(I):LSETC$=C1$(I):LSETD$=D1$(I):LSETE$=E1$(I):LSETG$=G1$(I):LSETRB$="":LSETCR$=CHR$(13)
3175 PUT #1,RECORD E
3180 IF S=1 THEN S=2:GOTO3185
3182 S=1:Z5=Z5+1:E=Z5
3185 NEXT I
3200 S=1:GOSUB120:GET#1,RECORD1:LSETRB$=MID$(STR$(TR),2,LEN(STR$(TR))-2):PUT#1,RECORD1
3205 S=2:GOSUB120:GET#1,RECORD1:LSETRB$=MID$(STR$(BO),2,LEN(STR$(BO))-2):PUT#1,RECORD1
3210 S=1:GOSUB120:GET#1,RECORD2:LSETRB$=MID$(STR$(SE),2,LEN(STR$(SE))-2):PUT#1,RECORD2
3211 IF BO>0 THEN 520
3212 IF UT=1 THENPRINTCHR$(7):PRINT"OBS! DATAFILEN SLUT! M]STE UT\KAS INNAN NY INMATNING KAN SKE!":PRINT:GOTO165
3220 IF LEFT$(AB$,1)="S" THEN 520 ELSE GOTO 3000
3300 PRINTCHR$(12):PRINTTAB(30);"* * INMATNING * *":PRINT
3302 PRINT"NR";A
3305 PRINT"MAX 30 TECKEN!  EFTER- o F\RNAMN ell F\RETAG /                              /"
3310 PRINT"MAX 20 TECKEN!  C/O (skriv {v C/O).......... /                    /"
3315 PRINT"MAX 30 TECKEN!  GATA ell BOX (skriv {v BOX). /                              /"
3320 PRINT"MAX 25 TECKEN!  POSTADRESS.................. /                         /"
3325 PRINT"MAX 13 TECKEN!  TEL NR...................... /             /"
3330 PRINTCHR$(5);:FORW=1TO5:PRINTCHR$(1);:NEXTW
3335 FORW=1TO46:PRINTCHR$(9);:NEXTW:PRINTCHR$(21);:INPUT#0,BB$:PRINTCHR$(5);
3337 PRINTCHR$(2);:FORW=1TO46:PRINTCHR$(9);:NEXTW:PRINTCHR$(21);:INPUTLINE#0,CC$:PRINTCHR$(5);
3340 PRINTCHR$(2);:FORW=1TO46:PRINTCHR$(9);:NEXTW:PRINTCHR$(21);:INPUTLINE#0,DD$:PRINTCHR$(5);
3350 PRINTCHR$(2);:FORW=1TO46:PRINTCHR$(9);:NEXTW:PRINTCHR$(21);:INPUTLINE#0,EE$:PRINTCHR$(5);
3360 PRINTCHR$(2);:FORW=1TO46:PRINTCHR$(9);:NEXTW:PRINTCHR$(21);:INPUTLINE#0,GG$
3370 PRINT:PRINT:PRINT"TRYCK RETURN F\R FORTSATT INMATNING!  ";
3375 PRINT"SKRIV 'S' F\R SLUT! ";:INPUTLINE#0,AB$:PRINT:RETURN
4000 PRINTCHR$(12):PRINT"* * BORTTAGNING * *":PRINT
4025 ON ERROR GOTO 11000
4030 INPUT"ANGE MED KUNDNUMMER VILKEN KUND SOM SKALL TAS BORT";A
4032 IF A<=1000 THENPRINTCHR$(7):PRINT"OBS! KUNDNUMRET SKALL VARA \VER 1000!":PRINT:GOTO4030
4035 PRINT
4095 Z5=A-1000
4096 IF Z5>TR THENPRINTCHR$(7):PRINT"KUNDNUMMER";A;"FINNS EJ!":GOTO520
4097 IF Z5/2<>INT(Z5/2) THEN S=1:Z5=(Z5+1)/2:E=Z5:GOTO4105
4098 S=2:Z5=Z5/2:E=Z5
4105 GOSUB120:GET#1,RECORDE
4107 IF B$="" THENPRINTCHR$(7):PRINT"KUNDNUMMER";A;"[R REDAN BORTTAGEN!":PRINT:GOTO4210
4115 PRINT B$;"  -TAS NU BORT UR REGISTRET!"
4116 KA=KA-1:BO=BO+1
4120 LSETB$="":LSETC$="":LSETD$="":LSETE$="":LSETG$=""
4125 PUT #1,RECORD E
4130 S=2:GOSUB120:GET#1,RECORD1:LSETRB$=MID$(STR$(BO),2,LEN(STR$(BO))-2):PUT#1,RECORD1
4200 PRINT:PRINT"KLART!":PRINT
4205 IF KA=0 THEN 520
4210 INPUT"\NSKAR DU G\RA EN TILL BORTTAGNING  J/N  ";AB$
4220 IF LEFT$(AB$,1)="J" THENPRINT:GOTO4025
4230 GOTO 520
5000 PRINTCHR$(12):PRINT"* * [NDRING * *":PRINT
5010 ON ERROR GOTO 11000
5020 INPUT"ANGE KUNDNUMRET";A
5025 IF A<=1000 THENPRINTCHR$(7):PRINT"OBS! KUNDNUMRET SKALL VARA \VER 1000!":GOTO5020
5030 PRINT
5050 Z5=A-1000
5060 IF Z5>TR THENPRINTCHR$(7):PRINT"KUNDNUMMER";A;"FINNS EJ!":GOTO520
5065 IF Z5/2<>INT(Z5/2) THEN S=1:Z5=(Z5+1)/2:E=Z5:GOTO5070
5066 S=2:Z5=Z5/2:E=Z5
5070 GOSUB120:GET#1,RECORDE
5075 IF B$="" THENPRINTCHR$(7):PRINT"KUNDNUMMER";A;"[R BORTTAGEN!":GOTO520
5083 PRINT
5090 PRINT "1.NAMN...: ";B$
5100 PRINT "2.C/O....: ";C$
5110 PRINT "3.GATA...: ";D$
5120 PRINT "4.POSTADR: ";E$
5125 PRINT "5.TEL NR.: ";G$:PRINT
5140 INPUT "VILKET NUMMER SKALL [NDRAS ";G:PRINT
5145 IF G<1 OR G>5 THEN PRINTCHR$(7):PRINT"FEL INSLAGET!":GOTO5140
5150 IF G=1 THEN GOSUB 5800
5160 IF G=2 THEN GOSUB 5820
5170 IF G=3 THEN GOSUB 5840
5180 IF G=4 THEN GOSUB 5860
5185 IF G=5 THEN GOSUB 5875
5210 PRINT:PRINT"\NSKAR DU G\RA N]GON MER [NDRING I KUNDNUMMER";A;" J/N ";:INPUTAB$
5220 IF LEFT$(AB$,1)="J" THEN 5083
5230 PUT #1, RECORD E
5240 GOTO 520
5800 PRINT B$;"  -[NDRAS TILL /                              /";
5810 FORW=1TO31:PRINTCHR$(4);:NEXTW:INPUT#0,BB$:PRINT:LSETB$=BB$:RETURN
5820 PRINT C$;"  -[NDRAS TILL /                    /";
5830 FORW=1TO21:PRINTCHR$(4);:NEXTW:INPUTLINE#0,CC$:PRINT:LSETC$=CC$:RETURN
5840 PRINT D$;"  -[NDRAS TILL /                              /";
5850 FORW=1TO31:PRINTCHR$(4);:NEXTW:INPUTLINE#0,DD$:PRINT:LSETD$=DD$:RETURN
5860 PRINT E$;"  -[NDRAS TILL /                         /";
5870 FORW=1TO26:PRINTCHR$(4);:NEXTW:INPUTLINE#0,EE$:PRINT:LSETE$=EE$:RETURN
5875 PRINT G$;"  -[NDRAS TILL /             /";
5876 FORW=1TO14:PRINTCHR$(4);:NEXTW:INPUTLINE#0,GG$:PRINT:LSETG$=GG$:RETURN
6000 IF KA=0 THEN 400 ELSE CLOSE 1:CHAIN "DEL-2.BAS"
10000 PRINT:PRINT "SLUT !":CLOSE 1
10010 PRINT CHR$(29);CHR$(29);CHR$(17):END
11000 IF ERR<>30 THEN ON ERROR GOTO
11010 PRINT CHR$(7):PRINT"FEL INSLAGET!"
11015 RESUME
