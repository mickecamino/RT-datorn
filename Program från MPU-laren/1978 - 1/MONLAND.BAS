1 REM DATUM 790811 NAMN "MOONL"
5 REM M]NLANDNING/PER SVENNBERG 1978
10 AA$="TRYCK RETURN"
25 DATA 10000,-1600,1000
30 X1=0
31 PRINT CHR$(12):PRINT:PRINT:PRINT:PRINT
32 PRINT "     *** M]NLANDNING ***"
33 PRINT 
35 PRINT "SKA JAG F\RKLARA REGLERNA?"
36 INPUT"SVARA JA ELLER NEJ";C$
40 PRINT CHR$(12)
45 IF C$="NEJ" THEN 395
50 IF C$="JA" THEN 75
61 PRINT 
75 PRINT "DU BEFINNER DIG OMBORD P] EN"
80 PRINT "M]NLANDARKAPSEL."
85 PRINT "H\JD \VER M]NYTAN:10000 METER"
90 PRINT "KAPSELNS FART    :-1600 M/SEK"
95 PRINT "TANKEN INNEH]LLER: 1000 LITER"
100 PRINT 
105 PRINT "DU SKA NU BROMSA DIG NED MOT"
110 PRINT "M]NYTAN GENOM ATT ANGE ETT"
115 PRINT "BROMSV[RDE MELLAN 1 OCH 75."
120 PRINT "V[RDE 7=KONSTANT FART"
125 PRINT "V[RDE <7=\KNING AV FARTEN"
130 PRINT "V[RDE >7=MINSKNING AV FARTEN"
135 PRINT "MAX BROMSV[RDE=75"
140 PRINT "NEGATIVA BROMSV[RDEN [R INTE"
145 PRINT "TILL]TNA!"
155 PRINT "INTERVALLET MELLAN VARJE BROMS-"
160 PRINT "NING = 1 SEKUND OCH EFTER VARJE"
165 PRINT "BROMSNING SKER MARKERING AV DIN"
170 PRINT "NYA FART OCH H\JD SAMT ]TERST]-"
175 PRINT "ENDE BR[NSLEM[NGD."
180 PRINT:PRINTAA$;:INPUTLINEAB$:PRINTCHR$(12)
185 PRINT "FARTEN ANGES NORMALT NEGATIVT." 
190 PRINT "OM DEN ANGES POSITIVT, S] BETY-"
195 PRINT "DER DETTA ATT DU BROMSAT S]"
200 PRINT "KRAFTIGT ATT KAPSELN [R P] V[G"
205 PRINT "BORT FR]N M]NEN."
210 PRINT "I DETTA FALL G[LLER F\LJANDE:"
215 PRINT "V[RDE 7=KONSTANT FART"
220 PRINT "V[RDE <7=MINSKNING AV FARTEN"
225 PRINT "V[RDE >7=\KNINGNG AV FARTEN"
235 PRINT "ANGIVELSERNA F\R FART, H\JD OCH"
240 PRINT "]TERST]ENDE BR[NSLEM[NGD ANGES"
245 PRINT "AVRUNDADE TILL 2 DECIMALER, MEN"
250 PRINT "I PROGRAMMET R[KNAS MED FLERA."
260 PRINT "F\R ATT G\RA DET HELA LITE MER"
265 PRINT "BESV[RLIGT, S] [R DET INTE BARA"
270 PRINT "DITT INSLAGNA BROMSV[RDE SOM P]"
275 PRINT "VERKAR HUR MYCKET FARTEN F\R[N-"
280 PRINT "DRAS,UTAN AKTUELL FART OCH H\JD"
285 PRINT "[R OCKS] FAKTORER SOM ING]R."
290 PRINT "DESSA TV] FAKTORER LIGGER SOM"
295 PRINT "VARSIN LOGARITMISK KURVA SOM I"
300 PRINT "VISS M]N MOTVERKAR VARANDRA."
305 PRINTAA$;:INPUTLINEAB$:PRINTCHR$(12)
314 PRINT 
315 PRINT "F\R EN HELT LYCKAD LANDNING"
320 PRINT "KR[VS F\RUTOM ATT FART OCH H\JD"
325 PRINT "SKALL STR[VA MOT NOLL,[VEN ATT"
330 PRINT "]TERST]ENDE BR[NSLEM[NGDEN [R"
335 PRINT "300 LITER ELLER MER."
340 PRINT 
345 PRINT "JA, D] [R DET BARA ATT STARTA."
350 PRINT "ELLER R[TTARE SAGT,ATT LANDA!"
355 PRINT 
360 PRINT "   **** LYCKA TILL! ****"
361 PRINT 
390 PRINT"F\R START: TRYCK RETURN";:INPUTLINEAB$
395 READ A9,H9,G9
400 RESTORE 
410  A=INT(100*A9+0.5)/100
415  H=INT(100*H9+0.5)/100
420  G=INT(10*G9+0.5)/10
424 PRINT 
425 PRINT "KAPSELNS FART= ";H
430 PRINT "         H\JD= ";A
435 PRINT "TANKINNEH]LL = ";G; "LITER"
436 IF G9=0 THEN 1225
440 INPUT "\NSKAT BROMSV[RDE= ",B
442 IF H9=0 THEN H9=-0.1
445 IF B>=0 THEN 465
450 PRINT "NEGATIVA BROMSV[RDEN [R INTE"
455 PRINT "TILL]TNA"
460 GOTO 440
465 IF B <=75 THEN 480
470 PRINT "MAX TILL]TET BROMSV[RDE = 75!!"
475 GOTO 440
480 IF B <= G9 THEN 500
485 PRINT "DU HAR BARA";G9;"LITER KVAR"
490 PRINT "BROMSV[RDET [NDRAT TILL:";G9
495  B=G9
500  G9=G9-B
505 IF X1=0 THEN 520
510 IF X1=1 THEN B=B/2
515 IF X1=2 THEN B=0
520  H4=ABS(H9)
525 IF B>7 THEN 545
530 IF B<7 THEN 605
531  H8=ABS(H9)+1
532  A8=1
533  H1=1
534  H7=1
535  A9=A9+H9
540 GOTO 670
545  H1=1.635*B*B ^ 2-11.445*B
546  H1=H1*A9 ^ (1/3)
550  H1=H1/(98*H4 ^ 0.25)
555  H2=(2*H9+H1)/2
560  H3=H9+H1
565 IF H3<= 0 THEN 590
570 IF H9>=0 THEN 590
575  H4=H9*(H4/H1)
580  A1=A9+H4
585 IF A1<0 THEN 1170
590  H8=ABS(H9)
592  H9=H3
593  H7=ABS(H2)
594  A8=A9
595  A9=A9+H2
600 GOTO 670
605  B1=7-B
610 IF B1<1.1 THEN B1=1.1
615  H1=A9 ^ 0.2*H4
616  H1=H1*((11.445-1.635*B)/7)^(1/B1)
620  H1=H1/(H4^0.2*A9 ^ (1/7))
625  H2=(2*H9-H1)/2
627  H7=ABS(H2)
630  H3=H9-H1
635 IF H9<0 THEN 660
640 IF H3>0 THEN 660
645  H4=ABS(H3)
650 IF H9>H4 THEN 660
655  H2=H2-2*H2
660  H8=ABS(H9)
662  H9=H3
663  A8=A9
665  A9=A9+H2
670 IF A9<0.05 THEN 920
671 IF G9<0 THEN 1225
675 IF B= 0 THEN B=13
680 FOR I=0 TO B
685  R=RND(0)
690 NEXT I
695 IF R> 0.49 THEN 410
700 IF R< 0.47 THEN 410
705 IF X1>0 THEN 785
710 IF R> 0.482 THEN 750
715 IF R< 0.478 THEN 750
720  X1=2
725 PRINT 
730 PRINT "**K A T A S T R O F !!**"
735 PRINT "RAKETERNA STREJKAR! DU KAN INTE"
740 PRINT "F] N]GON SOM HELST BROMSVERKAN!"
742 PRINT 
745 GOTO 410
750 PRINT 
755 PRINT "*** P R O B L E M !!***"
760 PRINT "RAKETERNA KR]NGLAR! DU KAN BARA"
765 PRINT "F] HALVA NORMALA BROMSVERKAN!"
770 PRINT 
775  X1=1
780 GOTO 410
785 IF X1> 1 THEN 870
790 IF R > 0.482 THEN 835
795 IF R < 0.478 THEN 835
800  X1=2
805 PRINT 
810 PRINT "*** K A T A S T R O F !!***"
815 PRINT "NU STREJKAR RAKETERNA HELT! DU"
820 PRINT "KAN INTE BROMSA DIN FART ALLS!"
825 PRINT 
830 GOTO 410
835 PRINT 
840 PRINT "********H U R R A !!*******"
845 PRINT "RAKETERNA T[NDER F\R FULLT IGEN!"
850 PRINT "NU G]R DET ATT BROMSA FARTEN!"
855 PRINT 
860  X1=0
865 GOTO 410
870 IF R>0.482 THEN 885
875 IF R<0.478 THEN 885
880 GOTO 835
885 PRINT 
890 PRINT " * F \ R B [ T T R I N G !! *"
895 PRINT "RAKETERNA T[NDER IGEN MEN BARA "
900 PRINT "MED REDUCERAD(=HALV) EFFEKT!"
905 PRINT 
910  X1=1
915 GOTO 410
920 IF A9<-0.5 THEN 1120
925 IF H9<-5 THEN 1040
930 PRINT 
935 PRINT "* * * * GRATTIS !! * * * *"
940 PRINT "DU HAR JUST LANDAT P] M]NEN!!"
945 IF H9<-0.5 THEN 995
950 IF G9<300 THEN 965
955 PRINT "DET VAR EN PERFEKT LANDNING!!"
960 GOTO 975
965 PRINT "MEN DU HAR F\R LITE BR[NSLE I"
970 PRINT "TANKARNA F\R ATT ]KA TILLBAKA!"
975 PRINT 
980  G=INT(10*G9+0.5)/10
985 PRINT "KVARVARANDE BR[NSLE: ";G;"LITER"
990 GOTO 1085
995 PRINT "MEN TYV[RR VAR FARTEN S] H\G "
1000 PRINT "VID NEDSLAGET ATT KAPSELNS "
1005 PRINT "STARTMOTORER F\R ]TERF[RDEN"
1010 PRINT "HAR BLIVIT SKADADE OCH EJ G]R"
1015 PRINT "ATT REPARERA!"
1020 PRINT 
1025  H=INT(10*H9+0.5)/10
1030 PRINT "FART VID NEDSLAGET:";H;"M/SEK"
1035 GOTO 980
1040  D=INT(10*(ABS(H9)/4)+0.5)/10
1045 PRINT 
1050 PRINT "* * * K R A S C H !!! * * *"
1055 PRINT "DU HAR JUST NU LYCKATS SKAPA EN"
1060 PRINT "NY KRATER P] M]NYTAN MED ETT"
1065 PRINT "DJUP AV "; D;"METER"
1070  H=INT(10*H9+0.5)/10
1075 PRINT 
1080 PRINT "FART VID NEDSLAGET:";H;"M/SEK"
1085 PRINT 
1090 PRINT "VILL DU F\RS\KA EN G]NG TILL?"
1095 INPUT"SVARA JA ELLER NEJ";C$
1096 PRINT CHR$(12)
1100 IF C$="JA"THENX1=0:PRINTCHR$(12):GOTO395
1105 IF C$="NEJ" THEN 1190
1120 IF H9<A9THEN1140
1125 IF A9<-1THEN1150
1130  D=INT(10*ABS(A9)+0.5)/10
1135 GOTO 1045
1140 IF H9<-1THEN1150
1145 GOTO 1130
1150  H9=ABS(H8-A8*H1/H7)
1155  D=INT(10*(H9^(1/3))+0.5)/10
1160  H9=H9-2*H9
1165 GOTO 1045
1170  H8=ABS(H9)
1175  A8=A9
1180  H7=ABS(H2)
1185 GOTO 1150
1190 PRINT :PRINT:PRINT:PRINT
1195 PRINT "* * * * * H E J  D ]! * * * * *"
1222 GOTO 1265
1225 PRINT "BR[NSLET SLUT!! DU ST\RTAR!!!"
1235 B=0
1240 G9=-1
1245 GOTO 605
1265 END
