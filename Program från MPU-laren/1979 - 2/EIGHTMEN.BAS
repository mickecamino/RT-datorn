3 REM ARKABLE PROGRAM WRITTEN BY RJC 4-10-69
5 REM VERSION 3 4/18/69
6 REM FIXAT TILL M6800 AV TBL  79-09-04 (FLEX)
8 DATA *,*,*,*,O,X,X,X,X
9 DATA X,X,X,X,O,*,*,*,*
10 DIM Z$(9),Y$(9)
15 PRINT CHR$(5)
17 PRINT "* * *   E I G H T  -  M E N   * * *"
18 PRINT
20 PRINT "DO YOU KNOW HOW TO PLAY";
30 GOSUB 680
40 ON F1 GOTO 240,70,50
50 PRINT "ANSWER EITHER YES OR NO"
60 GOTO 30
70 PRINT " ","POSITION NUMBERING-"
80 PRINT " ","123456789"
90 PRINT " ","****OXXXX"
100 PRINT
110 PRINT " ","OBJECT-"
120 PRINT "TO REVERSE THE POSITION OF THE *'S AND THE X'S."
130 PRINT "THE O IS AN EMPTY SPACE."
140 PRINT
150 PRINT " ","PUZZLE-"
160 PRINT "THE CENTER HOLE IS VACANT,"
165 PRINT "AND FOUR MEN OF EACH KIND ARE ON EACH "
170 PRINT "SIDE OF THE VACANT HOLE."
175 PRINT "YOU MOVE BY EITHER JUMPING TO THE NEXT HOLE"
180 PRINT "OR JUMPING OVER ONE MAN."
185 PRINT " BACKWARD MOVES ARE NOT PERMITTED."
190 PRINT "A SAMPLE MOVE WOULD BE 4,5 OR 3,5"
200 PRINT "YOU PLAY AGAINST YOURSELF,"
205 PRINT " M6800 JUST KEEPS YOU HONEST."
210 PRINT "TO CONCEDE AT ANY TIME TYPE 9,9 "
220 PRINT " ","GOOD LUCK"
225 INPUT "TYPE 'Y' WHEN READY TO GO. ",R$
230 REM RESTART
240 FOR I=1 TO 9
250 READ Z$(I)
260 NEXT I
270 FOR I=1 TO 9
280 READ Y$(I)
290 NEXT I
320 RESTORE
330 REM PLAYS THE GAME
340 PRINT "123456789"
341 FOR I=1 TO 9
342 PRINT Z$(I);
344 NEXT I
345 FOR I=1 TO 9
350 PRINT "    YOUR MOVE";
360 INPUT A,B
370 REM CHECKS FOR VALID MOVE
380 IF A=B GOTO 610
390 IF A+B>17 GOTO 580
400 IF Z$(B)<> "O" GOTO 580
410 IF Z$(A) = "X" GOTO 460
420 IF A>B GOTO 580
430 IF B-A>2 GOTO 580
440 LET Z$(B)="*"
450 GOTO 490
460 IF B>A GOTO 580
470 IF A-B>2 GOTO 580
480 LET Z$(B)="X"
490 LET Z$(A)="O"
500 REM CHECKS FOR A WIN
510 FOR I=1 TO 9 STEP 1
520 IF Z$(I) <> Y$(I) THEN 340
530 NEXT I
540 FOR I=1 TO 9
542 PRINT Z$(I);
544 NEXT I
550 PRINT "      ******* YOU DID IT *******!!!"
560 LET H=H+1
570 GOTO 630
580 PRINT "INVALID MOVE"
590 LET K=K+1
600 GOTO 350
610 PRINT "YOU CONCEDE"
620 LET G=G+1
630 PRINT "DO YOU WANT TO TRY AGAIN";
640 GOSUB 680
650 ON F1 GOTO 240,750,660
660 PRINT "JUST A YES OR NO PLEASE"
670 GOTO 640
680 INPUT R$
690 LET F1=1
700 IF R$="YES" THEN 740
710 LET F1=2
720 IF R$="NO" THEN 740
730 LET F1=3
740 RETURN
750 IF H>0 THEN 840
760 PRINT "YOU HAVEN'T SOLVED THE PUZZLE YET,"
765 PRINT " SO LET'S PLAY AGAIN ANYWAY"
770 PRINT "WHAT DO YOU SAY";
780 INPUT J$
790 IF J$ = "YES" THEN 240
800 IF J$ = "NO" THEN 830
810 PRINT "A YES OR NO WILL DO"
820 GOTO 780
830 PRINT "QUITTER"
840 PRINT "YOUR SCORE IS :"
845 PRINT H;" WINS,"
846 PRINT G;" LOSSES AND"
847 PRINT K;" INVALID MOVES."
850 PRINT "GOODBYE"
860 END
9000 REM
9100 FOR I=1 TO 200
9200 NEXT I
9300 POKE 8*4096+8,17
9400 SAVE
9500 STOP
