1 REM ** GOBANG (BYTE NOV 1979)
2 REM ** INKNAPPAD AV TBL 79-12-09 **
3 GOSUB 2000
4 DIM M(19,19),T(27,27),S(81)
6 FOR I=1 TO 81
10  S(I)=0
15 NEXT I
19 S(20)=1
20 S(10)=40
21 S(12)=30
22 S(13)=47
23 S(27)=15
24 S(28)=20
25 S(29)=10
26 S(30)=40
27 S(31)=50
28 S(32)=30
29 S(24)=1
30 S(36)=39
31 S(37)=65
32 S(38)=40
33 S(39)=70
34 S(40)=100
35 S(41)=60
36 S(42)=30
37 S(43)=30
38 S(44)=30
40 S(62)=41
41 S(72)=31
42 S(73)=11
43 S(74)=41
44 S(78)=51
45 S(80)=90
46 S(26)=21
47 S(79)=40
48 S(60)=21
49 S(61)=11
50 REM ** NOLLA BR[DET OCH B[STA DRAG ARRAYEN **
51 FOR I=1 TO 27
55  FOR J=1 TO 27
60   IF I<19 THEN IF J<19 THEN M(I,J)=0
70  NEXT J
75 NEXT I
76 C=-1
85 W=14
86 N=14
87 O=14
90 X=14
91 GOTO 300
95 GOSUB 800
96 REM ** BEG[R DRAG OCH KOLLA GILTIGHET **
97 PRINT "DITT DRAG ";
98 INPUT Z,Y
99 Y=Y+4
100 Z=Z+4
101 IF Y>23 THEN 97
102 IF Z>23 THEN 97
103 IF Y<5 THEN 97
104 IF Z<5 THEN 97
106 IF T(Y,Z)>0 THEN 97
110 T(Y,Z)=2
115 I=Y
120 J=Z
125 REM ** STUDERA SISTA TV[ DRAGEN **
127 GOSUB 1000
128 IF C<>-1 THEN 310
129 REM ** OM C=0 HAR DATORN F\RLORAT **
130 I=W
131 J=X
141 GOSUB 1000
145 REM ** SCANNA BR[DET F\R B[STA DRAG **
150 REM ** NOTERA BEGR[NSNINGARNA F\R UPPSNABBNING **
160 Q=-1
161 FOR I=N-1 TO O+1
162  FOR J=5 TO 23
200   IF T(I,J)>0 THEN 220
201   A=M(I-4,J-4)
205   IF A<Q THEN 220
210   W=I
215   X=J
216   Q=A
220  NEXT J
225 NEXT I
299 PRINT "MITT DRAG ";X-4,",";W-4
300 T(W,X)=1
301 IF M(W-4,X-4)<100 THEN 95
307 PRINT "JAG **VINNER**"
310 IF C=0 THEN PRINT "DU VINNER"
330 GOTO 50
799 REM ** SUB F\R ATT VISA BR[DET **
800 PRINT "    1234567890123456789"
805 FOR I=5 TO 23
810  IF I-4<10 THEN PRINT I-4;" ";
811  IF I-4>9 THEN PRINTI-4;
815  FOR J=5 TO 23
820   IF T(I,J)=0 THEN PRINT ":";
825   IF T(I,J)=1 THEN PRINT "X";
830   IF T(I,J)=2 THEN PRINT "O";
835  NEXT J
840 PRINT
845 NEXT I
850 RETURN
990 REM ** SUB BER[KNA B[STA DRAG **
991 REM ** SCANNA GENOM DRAG VID I,J **
992 REM ** F\R F KAVDRATER VID VARJE SIDA AV DRAGET **
993 REM ** I ]TTA RIKTNINGAR OCH **
994 REM ** UPPDATERA B[STA DRAG ARRAYEN **
1000 K=1
1001 L=-1
1002 IF I<N THEN IF I>5 THEN N=I
1003 IF I>O THEN IF I<23 THEN O=I
1004 REM ** UPPDATERA SCANN GR[NSERNA **
1005 U=I
1006 V=J
1007 REM ** I,J [R DRAG ATT KOLLA, D [R LOOP R[KNARE **
1008 REM ** K,L [R X,Y-RIKTNINGAR GENOM DRAGET **
1010 D=0
1011 D=D+1
1013 P=81
1020 REM ** KOLLA OM FORTFARANDE P] BR[DET **
1026 IF U>23 THEN 1090
1027 IF V>23 THEN 1090
1028 IF U<5 THEN 1090
1029 IF V<5 THEN 1090
1030 E=U-4
1031 G=V-4
1032 A=M(E,G)
1033 Q=T(U+K,V+L)
1034 REM ** BER[KNA POSITIONENS PRIORITET **
1035 R=T(U-K,V-L)*27+T(U-2*K,V-2*L)*9
1036 R=R+T(U-3*K,V-3*L)*3+T(U-4*K,V-4*L)
1037 B=Q*27+T(U+2*K,V+2*L)*9+T(U+3*K,V+3*L)*3
1038 IF R=80 THEN IF T(U,V)=2 THEN C=0
1039 IF T(U,V)<>0 THEN 1075
1040 REM ** S(R) [R PRIORITET. F\LJANDE [R UNDANTAG **
1041 REM ** SE TABELL 2 **
1042 IF R<14 THEN IF R>11 THEN IF Q=1 THEN P=37
1044 IF R>71 THEN IF B>53 THEN IF B<63 THEN P=80
1046 IF R>71 THEN IF B>71 THEN P=80
1048 IF R>53 THEN IF R<63 THEN IF Q=2 THEN P=72
1050 IF P=72 THEN IF R=60 THEN P=31
1052 IF Q<>2 THEN 1058
1053 IF R=78 THEN P=80
1054 IF R=79 THEN P=80
1056 IF R=41 THEN R=81
1058 IF R<42 THEN IF R>35 THEN IF Q=1 THEN P=41
1059 IF R<33 THEN IF R>29 THEN IF Q=1 THEN P=41
1060 IF R>53 THEN IF R<63 THEN IF B>71 THEN P=80
1061 IF R>38 THEN IF R<42 THEN IF Q=1 THEN R=40
1062 IF R>35 THEN IF R<45 THEN IF B>35 THEN IF B<45 THEN R=40
1063 IF R>27 THEN IF R<54 THEN IF B>38 THEN IF B<42 THEN R=40
1064 IF R=79 THEN IF A=51 THEN M(E,G)=41
1065 IF R=0 THEN R=81
1066 IF S(P)>S(R) THEN R=P
1067 IF S(R)-S(R)/10*10=1 THEN IF A-A/10*10=1 THEN IF S(R)<41 THEN R=74
1068 IF S(R)-S(R)/10*10=9 THEN IF A-A/10*10=9 THEN IF S(R)<65 THEN R=37
1069 REM ** UPPDATERA B[STA DRAG ARRAYEN **
1070 IF S(R)>M(E,G) THEN M(E,G)=S(R)
1075 IF D>4 THEN 1090
1081 U=U+K
1082 V=V+L
1085 GOTO 1011
1089 REM ** [NDRA RIKTNING **
1090 IF K=0 THEN IF L=-1 THEN RETURN
1095 IF K=-1 THEN IF L=-1 THEN K=0
1100 IF K=-1 THEN IF L=0 THEN L=-1
1105 IF K=-1 THEN IF L=1 THEN L=0
1110 IF K=0 THEN IF L=1 THEN K=-1
1115 IF K=1 THEN IF L=1 THEN K=0
1120 IF K=1 THEN IF L=0 THEN L=1
1125 IF K=1 THEN IF L=-1 THEN L=0
1130 GOTO 1005
2000 PRINT
2010 PRINT "* * *   G O B A N G   * * *"
2020 PRINT
2030 PRINT "GOBANG [R ETT LUFFARSCHACK MED"
2040 PRINT "19*19 RUTOR. DET G[LLER SOM"
2050 PRINT "VANLIGT ATT F] 5 I F\LJD. HORI-"
2060 PRINT "SONTELLT,VERTIKALT ELLER DIAGO-"
2070 PRINT "NALT. DATORN HAR 'X' DU 'O'"
2080 PRINT
2090 PRINT "DU GER ETT DRAG MED X,Y KOORDINATER."
2100 PRINT "EXEMPELVIS: 9,9"
2110 PRINT
2120 PRINT "SKRIV SIFFRAN 1 N[R DU VILL SPELA "
2130 INPUT Z
2140 RETURN
